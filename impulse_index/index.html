<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>impulse-index</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            font-family: 'Josefin Slab';
            font-size: 6vmin;
            color: white;
            background-color: black;
        }
        a:link {
          text-decoration: none;
          color: #FF8240;
        }

        a:visited {
          text-decoration: none;
          color: #FF8240;
        }

        a:hover {
          text-decoration: underline;
          color: #DFF180;
        }

        a:active {
          text-decoration: underline;
          color: #DFF180;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          top:0;
          left: 0;
          height: 100%;
          width: 100%;
          background-color: black;
        }
        div.footer {
          position: fixed;
          bottom: 0;
          width: 100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment"># version 300 es
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      in highp vec2 vTextureCoord;
      out vec4 fragColor;

      uniform vec2 iResolution;
      uniform float iTime;

      uniform sampler2D uSampler;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(fragColor, vTextureCoord*iResolution);
      }

      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      #define PI               3.141592654
      #define TAU             (2.0*PI)
      #define TIME            iTime
      #define RESOLUTION      iResolution
      #define SCA(a)          vec2(sin(a), cos(a))

      #define LESS(a,b,c)     mix(a,b,step(0.,c))
      #define SABS(x,k)       LESS((.5/k)*x*x+k*.5,abs(x),abs(x)-k)

      const vec2 sca0 = SCA(0.0);

      float hash(in vec2 co) {
        return fract(sin(dot(co.xy ,vec2(12.9898,58.233))) * 13758.5453);
      }

      float psin(float a) {
        return 0.5 + 0.5*sin(a);
      }

      float circle(vec2 p, float r) {
        return length(p) - r;
      }

      float box(vec2 p, vec2 b) {
        vec2 d = abs(p)-b;
        return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
      }

      float horseshoe(vec2 p, vec2 c, float r, vec2 w) {
        p.x = abs(p.x);
        float l = length(p);
        p = mat2(-c.x, c.y, c.y, c.x)*p;
        p = vec2((p.y>0.0)?p.x:l*sign(-c.x),(p.x>0.0)?p.y:l);
        p = vec2(p.x,abs(p.y-r))-w;
        return length(max(p,0.0)) + min(0.0,max(p.x,p.y));
      }

      void rot(inout vec2 p, float a) {
        float c = cos(a);
        float s = sin(a);
        p = vec2(c*p.x + s*p.y, -s*p.x + c*p.y);
      }

      float letteri(vec2 p) {
        p.y -= 0.25;
        return box(p, vec2(0.125, 0.75));
      }

      float letterm(vec2 p) {
        p.y = -p.y;
        float l = horseshoe(p - vec2(+0.5, 0.0), sca0, 0.5, vec2(0.5, 0.1));
        float r = horseshoe(p - vec2(-0.5, 0.0), sca0, 0.5, vec2(0.5, 0.1));
        return min(l, r);
      }

      float letterp(vec2 p) {
        float b = box(p - vec2(-0.45, -0.25), vec2(0.1, 0.75));
        float c = max(circle(p, 0.5), -circle(p, 0.3));
        return min(b, c);
      }

      float letteru(vec2 p) {
        return horseshoe(p - vec2(0.0, 0.125), sca0, 0.5, vec2(0.375, 0.1));
      }

      float letterl(vec2 p) {
        return box(p, vec2(0.125, 0.5));
      }

      float letters(vec2 p) {
        rot(p, -PI/6.0);
        rot(p, -PI/2.0);
        float u = horseshoe(p - vec2(-0.25*3.0/4.0, -0.125/2.0), sca0, 0.375, vec2(0.2, 0.1)) - 0.0;
        rot(p, PI);
        float l = horseshoe(p - vec2(-0.25*3.0/4.0, -0.125/2.0), sca0, 0.375, vec2(0.2, 0.1));
        return min(u,l);
      }

      float lettere(vec2 p) {
        return min(box(p, vec2(0.4, 0.1)), max(circle(p, 0.5), -circle(p, 0.3)));
      }

      float impulse(vec2 p) {
        p.x += 0.6;
        const float oi = -3.00;
        const float om = -1.65;
        const float op = +0.10;
        const float ou = +1.25;
        const float ol = +2.10;
        const float os = +2.80;
        const float oe = +3.85;
        float di = letteri(p - vec2(oi, 0.0));
        float dm = letterm(p - vec2(om, 0.0));
        float dp = letterp(p - vec2(op, 0.0));
        float du = letteru(p - vec2(ou, 0.0));
        float dl = letterl(p - vec2(ol, 0.0));
        float ds = letters(p - vec2(os, 0.0));
        float de = lettere(p - vec2(oe, 0.0));
        float oo = 0.1;
        float dx = abs(p.y) - oo;
        dx = abs(dx) - oo*0.5;
        float d = 1000000.0;
        d = min(d, di);
        d = min(d, dm);
        d = min(d, dp);
        d = min(d, du);
        d = min(d, dl);
        d = min(d, ds);
        d = min(d, de);
        d = max(d, -dx);

        return d;
      }

      float onoise(vec2 x) {
        x *= 0.5;
        float a = sin(x.x);
        float b = sin(x.y);
        float c = mix(a, b, psin(TAU/1.25*(a*b+a+b)));

        return c;
      }

      float cell0(vec2 p) {
        float d1 = length(p+vec2(0.5)) - 0.5;
        float d2 = length(p-vec2(0.5)) - 0.5;
        float d = min(d1, d2);
        return d;
      }

      float cell1(vec2 p) {
        float d1 = abs(p.x);
        float d2 = abs(p.y);
        float d = min(d1, d2);
        return d;
      }

      float tnoise(vec2 p) {
        p *= 0.125;
        p += 0.5;
        vec2 nn = floor(p);
        vec2 pp = fract(p) - 0.5;
        float r = hash(nn);
        vec2 rot = -1.0 + 2.0*vec2(step(fract(r*13.0), 0.5), step(fract(r*23.0), 0.5));
        pp *= rot;
        const float w = 0.1;
        float d = 1E6;
        if(r < 0.75) {
          d = cell0(pp);
        } else  {
          d = cell1(pp);
        }
        d = abs(d) - w;
        float h = smoothstep(0.0, w, -d);
        return h;
      }

      float vnoise(vec2 x) {
        vec2 i = floor(x);
        vec2 w = fract(x);

      #if 1
        // quintic interpolation
        vec2 u = w*w*w*(w*(w*6.0-15.0)+10.0);
      #else
        // cubic interpolation
        vec2 u = w*w*(3.0-2.0*w);
      #endif

        float a = hash(i+vec2(0.0,0.0));
        float b = hash(i+vec2(1.0,0.0));
        float c = hash(i+vec2(0.0,1.0));
        float d = hash(i+vec2(1.0,1.0));

        float k0 =   a;
        float k1 =   b - a;
        float k2 =   c - a;
        float k3 =   d - c + a - b;

        float aa = mix(a, b, u.x);
        float bb = mix(c, d, u.x);
        float cc = mix(aa, bb, u.y);

        return k0 + k1*u.x + k2*u.y + k3*u.x*u.y;
      }

      float fbm(vec2 p) {
        const int mid = 3;
        const int mx = 7;
        const float aa = 0.4;
        const float pp = 2.3;
        const vec2 oo = -vec2(1.23, 1.5);
        const float rr = 1.2;

        vec2 op = p;

        float h = 0.0;
        float d = 0.0;
        float a = 1.0;

        for (int i = 0; i < mid; ++i) {
          h += a*onoise(p);
          d += (a);
          a *= aa;
          p += oo;
          p *= pp;
          rot(p, rr);
        }

        for (int i = mid; i < mx; ++i) {
          h += a*tnoise(p);
          d += (a);
          a *= aa;
          p += oo;
          p *= pp;
          rot(p, rr);
        }

        return 0.5*mix(-1.0, 1.0, smoothstep(0.0, 1.2, (vnoise(0.50*op))))*(h/d);
      }

      float warp(vec2 p) {
        vec2 v = vec2(fbm(p), fbm(p+10.7*vec2(1.0, 1.0)));
        rot(v, 1.0+TIME*0.125);
        return mix(0., 1.0, v.x - v.y);
      }

      float height(vec2 p) {
        float a = 0.005*TIME;
        p += 5.0*vec2(cos(a), sin(sqrt(0.5)*a));
        p *= 2.0;
        p += 13.0;
        float h = warp(p);
        float rs = 3.0;
      //  return 0.75*tanh(rs*h)/rs;
        return 0.75*h;
      }

      vec3 normal(vec2 p) {
        vec2 eps = -vec2(0.0001, 0.0);

        vec3 n;

        n.x = height(p + eps.xy) - height(p - eps.xy);
        n.y = 2.0*eps.x;
        n.z = height(p + eps.yx) - height(p - eps.yx);


        return normalize(n);
      }

      vec3 postProcess(vec3 col, vec2 q)  {
        col=pow(clamp(col,0.0,1.0),vec3(0.75));
        col=col*0.6+0.4*col*col*(3.0-2.0*col);  // contrast
        col=mix(col, vec3(dot(col, vec3(0.33))), -0.4);  // satuation
        col*=0.5+0.5*pow(19.0*q.x*q.y*(1.0-q.x)*(1.0-q.y),0.7);  // vigneting
        return col;
      }

      float df(vec2 p) {
        p *= 3.0;
        float d = impulse(p);
        return d;
      }

      void mainImage(out vec4 fragColor, in vec2 fragCoord) {
        vec2 q = fragCoord/RESOLUTION.xy;
        vec2 p = -1. + 2. * q;
        p.x *= RESOLUTION.x/RESOLUTION.y;

        const vec3 lp1 = vec3(0.8, -0.75, 0.8);
        const vec3 lp2 = vec3(-0., -1.5, -1.0);

        float aa = 10.0/RESOLUTION.y;

        vec3 col = vec3(0.0);
        float h = height(p);
        vec3 pp = vec3(p.x, h, p.y);
        vec3 ld1 = normalize(lp1 - pp);
        vec3 ld2 = normalize(lp2 - pp);

        vec3 n = normal(p);
        float diff1 = max(dot(ld1, n), 0.0);
        float diff2 = max(dot(ld2, n), 0.0);

        const vec3 baseCol1 = vec3(0.6, 0.8, 1.0);
        const vec3 baseCol2 = sqrt(baseCol1.zyx);

        col += baseCol1*pow(diff1, 16.0);
        col += 0.1*baseCol1*pow(diff1, 4.0);
        col += 0.15*baseCol2*pow(diff2,8.0);
        col += 0.015*baseCol2*pow(diff2, 2.0);

        col = clamp(col, 0.0, 1.0);
        col = mix(0.05*baseCol1, col, 1.0 - (1.0 - 0.5*diff1)*exp(- 2.0*smoothstep(-.1, 0.05, (h))));

        float d = df(p);
        float bd = abs(d - 0.03) - 0.015;

        float shd = pow(psin(-0.25*TIME+(p.x-p.y)*1.5), 4.0);
        col += 1.0*baseCol1*(exp(-20.0*bd))*shd;
        col += 0.5*baseCol1*(exp(-1.0*bd))*shd;
        col -= 0.125*vec3(1.0)*(exp(-5.0*d));
        col = clamp(col, 0.0, 1.0);
        col = mix(col, vec3(1.0), smoothstep(0.0, aa, -d));

        col = postProcess(col, q);

        const float fadeIn = 6.0;
        col *= smoothstep(0.0, fadeIn*fadeIn, TIME*TIME);

        fragColor = vec4(col, 1.0);
      }
    </script>

    <script id="shader-vs" type="x-shader/x-vertex"># version 300 es
      in vec2 pos;
      in vec2 texPos;

      out highp vec2 vTextureCoord;

      void main(void) {
        gl_Position=vec4(pos,0.0,1.0);
        vTextureCoord=texPos;
      }
    </script>
  </head>

  <body onload="init()">
    <div id="information" class="footer" style="display:flex;justify-content:center;align-items:center;">
      <div>
        <p style="font-size: 6vmin;">
          impulse-0001: <a href="https://mrange.github.io/impulse_0001/" target="_bland">a study of glass</a></br>
          impulse-0005: <a href="https://mrange.github.io/impulse_0005/" target="_bland">psychedelic dreams in 2d</a></br>
          impulse-0006: <a href="https://mrange.github.io/impulse_0006/" target="_bland">once upon a time... space</a></br>
        </p>
        <p style="font-size: 2vmin;">
          Suspicously missing 0002-0004 hasn't been successfully ported to WebGL yet.
        </p>
      </div>
    </div>
    <div style="display:flex;justify-content:center;align-items:center;">
      <div>
        <canvas id="glcanvas" class="gl">
          Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
        </canvas>
      </div>
    </div>
  </body>
</html>