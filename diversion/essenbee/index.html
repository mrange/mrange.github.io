<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>infinite alfs</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          left:0;
          top:0;
          width:100%;
          height:100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;
      uniform sampler2D iChannel0;

      varying highp vec2 vTextureCoord;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      // Created by mrange/2019
      // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

      // Technique from: https://www.shadertoy.com/view/4slGWH

      vec4 sample(vec2 p)
      {
        p *= 10.0;
        p.x = mod(p.x, 1.0);
        vec4 col = texture2D(iChannel0, p);
        float yscale = 1.0 / (1.0 + max(p.y - 1.0, 0.0));
        return vec4(col.xyz, col.w*yscale);
      }

      void rot(inout vec2 p, float a)
      {
        float c = cos(a);
        float s = sin(a);
        p = vec2(c*p.x + s*p.y, -s*p.x + c*p.y);
      }

      void mainImage(out vec4 fragColor, in vec2 fragCoord)
      {
        vec2 p = (2.0*fragCoord-iResolution.xy)/iResolution.y;

        float time = iTime;

        // zoom
        p = vec2(0.5, -0.05) + p*0.75*pow(0.9, 20.0*(0.5+0.5*cos(0.25*time)));

        vec4 col = vec4(0.0);
        vec3 ss = mix(vec3(0.2,0.2, 1.0), vec3(0.5,-0.2,0.5), 0.5+0.5*sin(0.5*time));

        vec2 c = vec2(-0.76, 0.15);
        rot(c, 0.2*sin(time/6.0));
        float f = 0.0;
        vec2 z = p;

        const int maxIter = 130;
        for(int i=0; i<=maxIter; ++i)
        {
          if((dot(z,z)>4.0) || (col.w>0.05)) break;

          // fc(z) = zÂ² + c
          z = vec2(z.x*z.x - z.y*z.y, 2.0*z.x*z.y) + c;

          col = sample(ss.xy + ss.z*z)*sqrt((float(maxIter)-f)/float(maxIter));
          f += 1.0;
        }

        vec3 bg = 0.3*(vec3(97.0,104.0,246.0)/255.0)*sqrt(f/float(maxIter));

        col.xyz = mix(bg, col.xyz, col.w);

        fragColor = vec4(col.xyz, 1.0);
      }
    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <canvas id="glcanvas" class="gl" width="1920" height="1200">
      Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>
    <span style="display:flex;justify-content:center;align-content:center;font-size: 200px">
        infinite alfs
    </span>
  </body>
</html>