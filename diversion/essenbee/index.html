<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>infinite alfs</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          left:0;
          top:0;
          width:100%;
          height:100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;
      uniform sampler2D iChannel0;

      varying highp vec2 vTextureCoord;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      // Created by mrange/2019
      // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

      // Technique from: https://www.shadertoy.com/view/4slGWH

      vec4 sample(vec2 p)
      {
        p *= 5.0;
        float xscale = 1.0 - smoothstep(1.0, 20.0, abs(p.x));
        float yscale = 1.0 - smoothstep(1.0, 5.0, p.y);
        float scale = xscale*yscale;
        p.x = mod(p.x, 1.0);
        if (scale < 0.1 || p.y < 0.0) return vec4(0.0);

        vec4 col = texture2D(iChannel0, p);
        return vec4(col.xyz, col.w*scale);
      }

      void rot(inout vec2 p, float a)
      {
        float c = cos(a);
        float s = sin(a);
        p = vec2(c*p.x + s*p.y, -s*p.x + c*p.y);
      }

      void mainImage(out vec4 fragColor, in vec2 fragCoord)
      {
        vec2 p = (2.0*fragCoord-iResolution.xy)/iResolution.y;

        float time = iTime;

        // zoom
        p = vec2(0.5, -0.05) + p*0.75*pow(0.9, 20.0*(0.5+0.5*cos(0.3*sqrt(2.0)*time)));

        vec4 col = vec4(0.0);
        vec3 ss = mix(vec3(0.2, 0.2, 0.5), vec3(0.2,-0.2,1.0), 2.2 + 1.25*sin(time/2.0));

        vec2 c = vec2(-0.76, 0.15);
        rot(c, 0.2*sin(time*sqrt(3.0)/12.0));
        float f = 0.0;
        vec2 z = p;

        float transparency = 1.0;

        vec3 bg = 0.3*(vec3(97.0,104.0,246.0)/255.0);

        const int maxIter = 100;
        const float maxIterF = float(maxIter);
        for(int i=0; i<=maxIter; ++i)
        {
          float re2 = z.x*z.x;
          float im2 = z.y*z.y;
          if((re2 + im2>4.0) || (transparency<0.1)) break;
          float reim = z.x*z.y;

          z = vec2(re2 - im2, 2.0*reim) + c;

          float shade = (1.0-0.9*f/maxIterF);

          vec4 sample = sample(ss.xy + ss.z*z);
          sample.xyz = mix(bg, sample.xyz, shade);

          transparency *= 1.0 - clamp(sample.w, 0.0, 1.0);
          vec4 newCol = vec4(col.xyz + sample.xyz*(1.0 - col.w)*sample.w, 1.0 - transparency);

          col = newCol;

          f += 1.0;
        }

        col.xyz = mix(bg, col.xyz, col.w);

        fragColor = vec4(col.xyz, 1.0);
      }

    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <img id="image0" src="essenbee.png" style="visibility: collapse"/>
    <canvas id="glcanvas" class="gl">
      Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>
  </body>
</html>