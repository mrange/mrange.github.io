<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>Parametric curves</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="demo-system-v2.js" type="text/javascript"></script>
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Josefin+Slab&display=swap" rel="stylesheet">
    <link href="https://fonts.googleapis.com/css2?family=Faster+One&display=swap" rel="stylesheet">
    <style>
        body {
            font-family: 'Josefin Slab';
            font-size: 2vw;
            color: white;
            background-color: black;
        }
        a:link {
          text-decoration: none;
          color: #FF8240;
        }
        a:visited {
          text-decoration: none;
          color: #FF8240;
        }
        a:hover {
          text-decoration: underline;
          color: #DFF180;
        }
        a:active {
          text-decoration: underline;
          color: #DFF180;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          top:0;
          left: 0;
          height: 100%;
          width: 100%;
          background-color: black;
        }
        div.center-flex {
          display: flex;
          align-items: center;
          justify-content: center;
        }
    </style>

    <script id="vs_default" type="x-shader/x-vertex">
      precision highp float;

      uniform float time;

      in vec4 a_position;
      in vec2 a_texcoord;

      out vec2 v_texcoord;

      void main() {
        gl_Position = a_position;
        v_texcoord  = a_texcoord;
      }
    </script>

    <script id="fs_present" type="x-shader/x-fragment">
      precision highp float;

      uniform vec2 resolution;

      uniform sampler2D prev_pass ;
      uniform sampler2D overlay   ;

      in vec2 v_texcoord  ;
      out vec4 frag_color ;

      vec4 toverlay(vec2 p) {
        vec2 sz = vec2(textureSize(overlay, 0));
        p.x *= sz.y/sz.x;
        p *= 0.5;
        p *= resolution.y/sz.y;
        p += 0.5;
        p.y = 1.0-p.y;
        return texture(overlay, p);
      }

      void main(void) {
        vec2 q = v_texcoord;
        vec2 p = -1. + 2. * q;
        p.x *= resolution.x/resolution.y;

        vec4 pcol = texture(prev_pass, v_texcoord);
        vec4 ocol = toverlay(p);
        vec3 col = pcol.xyz;
        col = mix(col, ocol.xyz, ocol.w);
        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_postproc" type="x-shader/x-fragment">
      precision highp float;

      uniform float time;
      uniform vec2 resolution;

      uniform sampler2D prev_frame;
      uniform sampler2D prev_pass;

      in vec2 v_texcoord;

      out vec4 fragColor;

      #define PI          3.141592654
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      #define RESOLUTION  resolution

      #define BLUR

      // License: Unknown, author: nmz (twitter: @stormoid), found: https://www.shadertoy.com/view/NdfyRM
      float sRGB(float t) { return mix(1.055*pow(t, 1./2.4) - 0.055, 12.92*t, step(t, 0.0031308)); }
      // License: Unknown, author: nmz (twitter: @stormoid), found: https://www.shadertoy.com/view/NdfyRM
      vec3 sRGB(in vec3 c) { return vec3 (sRGB(c.x), sRGB(c.y), sRGB(c.z)); }

      void main(void) {
        vec2 q = v_texcoord;
        vec2 p = -1.0+2.0*q;

      #ifdef BLUR
        const int c = 3;

        vec2 pp = p;
        pp *= 0.9+0.0;

        vec2 pq = 0.5+0.5*pp;

        vec2 aa = 10.0/RESOLUTION.xy;
        vec2 start = pq-float(c)*aa;
        vec3 bcol = vec3(0.0);
        for (int y = -c; y <= c; ++y) {
          vec2 pp = start;
          for (int x = -c; x <= c; ++x) {
            vec4 fcol = texture(prev_frame, pp);
            bcol += fcol.xyz;
            pp.x += aa.x;
          }
          start.y += aa.y;
        }

        bcol /= float((2*c+1)*(2*c+1));

        vec4 pcol = texture(prev_pass,v_texcoord);
        vec3 col = pcol.xyz;
        col = sRGB(col);
        col = mix(col, bcol, 0.6)*1.2;
      #else
        vec4 pcol = texture(prev_pass,v_texcoord);
        vec3 col = pcol.xyz;
        col = sRGB(col);
      #endif

        fragColor = vec4(col, pcol.w);
      }
    </script>

    <script id="vs_curves" type="x-shader/x-vertex">
      precision highp float;

      uniform float time;
      uniform mat4 mvp;

      in vec4 a_position;
      in vec2 a_texcoord;

      out vec2        v_texcoord;
      flat out float  v_ratio   ;
      flat out vec2   v_i2      ;

      #define PI          3.141592654
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      #define FROM_CURVE curve3
      #define TO_CURVE   curve2

      const vec2 dimn = 2.0*vec2(120.0, 20.0);
      vec2 i2(float i) {
        return vec2(mod(i, dimn.x)/dimn.x, floor(i/dimn.x)/dimn.y);
      }

      const vec2 dimu = vec2(-PI, PI);
      const vec2 dimv = dimu;

      const vec2 du = vec2(
          (dimu.y - dimu.x)/dimn.x
        , (dimv.y - dimv.x)/dimn.y
        );

      const vec2 dux = vec2(du.x, 0.0);
      const vec2 duy = vec2(0.0, du.y);

      vec2 uv(vec2 i2) {
        float u   = mix(dimu.x, dimu.y, i2.x);
        float v   = mix(dimv.x, dimv.y, i2.y);
        return vec2(u, v);
      }

      vec3 curve0(vec2 p) {
        p *= vec2(2.0, 2.0);
        float r = mix(1.0, 3.0, 0.5+0.5*cos(p.x*0.5));
        vec3 cp = vec3(
            r*cos(p.x)*sin(p.y)
          , r*sin(p.x)*sin(p.y)
          , r*cos(p.y)
          );
        return cp;
      }

      vec3 curve1(vec2 p) {
        p *= vec2(1.0, 4.0);
        float a = 0.5;
        float b = 2.0;
        vec3 cp = vec3(
            (b + a*cos(p.y))*cos(p.x)
          , (b + a*cos(p.y))*sin(p.x)
          , a*sin(p.y)
          );
        return cp;
      }

      vec3 curve2(vec2 p) {
        p *= vec2(4.0, 4.0);
        float a = 0.5;
        float b = 2.0+cos(0.25*p.x);
        vec3 cp = vec3(
            (b + a*cos(p.y))*cos(p.x)
          , (b + a*cos(p.y))*sin(p.x)
          , a*sin(p.y)+2.0*sin(0.25*p.x)
          );
        return cp;
      }

      vec3 curve3(vec2 p) {
        p *= vec2(1.0, 4.0);
        float a = 0.25;
        float b = 0.75;
        float r = (b + a*cos(p.y));
        vec3 cp = vec3(
            r*cos(2.0*p.x)*(3.0+cos(3.0*p.x))
          , r*sin(2.0*p.x)*(3.0+cos(3.0*p.x))
          , r*sin(3.0*p.x)+PI*a*sin(p.y)
          );
        return cp;
      }

      vec3 curve4(vec2 p) {
        p *= vec2(1.0, 2.0);
        float r = 3.0;
        vec3 cp = vec3(
            r*cos(p.x)*sin(p.y)
          , r*sin(p.x)*sin(p.y)
          , r*cos(p.y)
          );
        return cp;
      }

      void main() {

        mat2 rxy = ROT(0.1*time);
        mat2 rxz = ROT(0.35*time);
        mat2 ryz = ROT(0.25*time);
        vec3 off =  vec3(0.0, 0.0, 4.0);

        float i   = float(gl_InstanceID);
        vec2  i2  = i2(floor(i/2.0));
        vec2 uv   = uv(i2);
        vec2 duv  = mod(i, 2.0) >= 1.0 ? dux : duy;

        vec3 p0_1 = FROM_CURVE(uv);
        vec3 p1_1 = FROM_CURVE(uv+duv);

        vec3 p0_2 = TO_CURVE(uv);
        vec3 p1_2 = TO_CURVE(uv+duv);

        float f   = smoothstep(-0.25, 0.25, sin(0.5*time));
      //  f = 1.0;
        vec3 p0   = mix(p0_1, p0_2, f);
        vec3 p1   = mix(p1_1, p1_2, f);

        p0.xy *= rxy;
        p0.xz *= rxz;
        p0.yz *= ryz;

        p1.xy *= rxy;
        p1.xz *= rxz;
        p1.yz *= ryz;

        const float lw = 0.175;

        vec3 c  = (p0+p1)*0.5;
        vec3 d  = p1-p0;
        float l = length(d);

        vec3 up = vec3(0.0, 0.0, -1.0);
        vec3 ww = d/l;
        vec3 uu = normalize(cross(ww, up));
        vec3 vv = cross(uu, ww);


        vec4 p = a_position;
        mat3 m = mat3(ww, uu, vv);
        p.xy *= 0.5*vec2(l, lw);
        p.xyz *= transpose(m);
        p.xyz -= c;

        gl_Position = mvp * p;

        v_texcoord  = a_texcoord;
        v_ratio     = l/lw;
        v_i2        = i2;
      }
    </script>

    <script id="fs_curves" type="x-shader/x-fragment">
      precision highp float;

      uniform float time;
      uniform vec2 resolution;

      in vec2       v_texcoord;
      flat in float v_ratio   ;
      flat in vec2  v_i2      ;

      out vec4 fragColor;

      #define PI      3.141592654
      #define PCOS(x) (0.5+0.5*cos(x))

      float segmentx(vec2 p, float w) {
        p = abs(p);
        float d0 = p.y;
        float d1 = length(p-vec2(w, 0.0));
        return p.x > w ? d1 : d0;
      }

      // License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
      const vec4 hsv2rgb_K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
      vec3 hsv2rgb(vec3 c) {
        vec3 p = abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www);
        return c.z * mix(hsv2rgb_K.xxx, clamp(p - hsv2rgb_K.xxx, 0.0, 1.0), c.y);
      }
      // License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
      //  Macro version of above to enable compile-time constants
      #define HSV2RGB(c)  (c.z * mix(hsv2rgb_K.xxx, clamp(abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www) - hsv2rgb_K.xxx, 0.0, 1.0), c.y))

      void main() {
        float originalZ = gl_FragCoord.z / gl_FragCoord.w;
        vec2 q = v_texcoord;
        vec2 p = -1. + 2. * q;

        float r = v_ratio;
        vec2  i2= v_i2;
        p.x *= r;

        vec3 col = vec3(0.0);
        float l = length(p);
        float h = i2.x+0.25*i2.y;
        float s = 0.25;
        vec3 gcol0 = hsv2rgb(vec3(h, 0.55, s));
        vec3 gcol1 = hsv2rgb(vec3(h, 0.95, s));
        float sl = mix(0.0, 1.0, smoothstep(0.25, 1.0, cos(2.0*PI*i2.x+time)))*r;
        float ds = segmentx(p, sl); //PCOS(4.0*PI*i2.x+time)
        col += 4.0*gcol0*exp(-9.0*ds);
        col += 2.0*gcol1*exp(-6.0*ds);
        col *= 3.0/(originalZ);
      //  col *= 8.0*exp(-0.5*originalZ);
        fragColor = vec4(col, 1.0);
      }
    </script>

    <script type = "text/javascript">
      const analyze_audio   = false;
      const demo_system     = new DemoSystemV2(analyze_audio);

      const global_uniforms = [];

      const all_textures = {
        overlay : {
          image : (width, height) => create_text_image(width, 0.25*height, [0.5, 0.5, "Impulse"]),
        }
      }

      const all_scenes = {
        parametric_curve_scene : {
          passes:[
            {
              vs: "vs_curves",
              fs: "fs_curves",
              requires_clear: true,
              instances: 4800,
              pre_render: (gl, time, scene, pass) => {
                gl.enable(gl.BLEND);
                gl.blendFuncSeparate(gl.SRC_ALPHA, gl.DST_ALPHA, gl.ONE, gl.ONE);
                gl.blendEquationSeparate(gl.FUNC_ADD, gl.MAX);

                gl.uniformMatrix4fv(pass.uniformLocations.mvp, false, mvp);
              },
            },
            {
              vs: "vs_default",
              fs: "fs_postproc",
              pre_render: (gl, time, scene, pass) => {
                gl.disable(gl.BLEND);
              }
            },
          ],
          present_pass: {
            vs: "vs_default",
            fs: "fs_present",
            pre_render: (gl, time, scene, pass) => {
              gl.disable(gl.BLEND);
              // Texture 0 - 3 are reserved
              gl.activeTexture(gl.TEXTURE4);
              gl.bindTexture(gl.TEXTURE_2D, all_textures.overlay.texture);
              gl.uniform1i(pass.uniformLocations.overlay, 4);
            },
          },
          uniforms: ["overlay", "mvp"],
        }
      };

      // Called after initialization of all shaders are complete
      function on_init_complete(width, height) {
        globalThis.projection  = projection_matrix4(Math.PI/3.0, width/height, 1, 1000);
        globalThis.view        = look_at_matrix4(vector3(0,0,7), vector3(0,0,0), vector3(0,1,0));
        globalThis.mvp         = multiply_matrix4(view, projection);
        //printMatrix4(globalThis.mvp);

        const label = document.getElementById("info_label");
        label.textContent = "Ready, click play to run";

        const music = document.getElementById("music");
        music.style.visibility = "visible";
      }

      // Called after user clicked play
      function on_started() {
        const information = document.getElementById("information");
        information.style.display = "none";
      }

      // Called before each shader is compiled
      function on_loading_scene(key) {
        const label = document.getElementById("info_label");
        label.textContent = "Compiling: " + key;
      }

      // Called each frame to select which scene to render
      function on_select_scene(gl, time) {
        return all_scenes.parametric_curve_scene;
      }

    </script>
  </head>

  <body onload="demo_system.run_demo()">
    <audio id="music" style="visibility: hidden;" controls src="blear-moon--these-walls-are-talking.mp3"></audio>
    <div id="information" style="margin-left: 16pt;">
      <h1 style="font-family: 'Faster One';" >Parametric curves</h1>

      <p>Music: <a target="_blank" href="https://freemusicarchive.org/music/Blear_Moon/Town_of_Two_Houses/These_Walls_Are_Talking">These walls are talking</a> by <a target="_blank" href="https://freemusicarchive.org/music/Blear_Moon">Blear Moon</a> licensed under <a target="_blank" href="https://creativecommons.org/licenses/by-nc/4.0/">CC BY-NC 4.0</a></p>

      <p id="info_label">Please wait... </p>
    </div>
    <div class="center-flex">
      <div>
        <canvas id="screen_canvas" class="gl">
          Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
        </canvas>
      </div>
    </div>
    <canvas id="offscreen_canvas" style="display: none">

    </canvas>
  </body>
</html>