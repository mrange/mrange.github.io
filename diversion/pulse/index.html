<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>Electric Pulse</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          left:0;
          top:0;
          width:100%;
          height:100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;
      uniform sampler2D iChannel0;
      uniform sampler2D iChannel1;
      uniform sampler2D iChannel2;

      varying highp vec2 vTextureCoord;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      // Created by mrange/2019
      // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

      float pulsetop(vec2 p) {
        //  ivec2 ivec = textureSize(iChannel0, 0);
        //  p.x *= float(ivec.y)/ivec.x;
          p.x *= 234.0/287.0;
          p.y = -p.y;
          p*=1.0;
          p+=vec2(1.00, 1.00);
          p*=0.5;
          vec4 t = texture2D(iChannel0, p);
          float d = t.x + (1.0/256.0)*t.y + (1.0/(256.0*256.0))*t.z;
          return -2.0*d + 1.0;
        }

        float pulsebottom(vec2 p) {
        //  ivec2 ivec = textureSize(iChannel1, 0);
        //  p.x *= float(ivec.y)/ivec.x;
          p.x *= 232.0/262.0;
          p.y = -p.y;
          p*=1.0;
          p+=vec2(1.00, 1.00);
          p*=0.5;
          vec4 t = texture2D(iChannel1, p);
          float d = t.x + (1.0/256.0)*t.y + (1.0/(256.0*256.0))*t.z;
          return -2.0*d + 1.0;
        }

        float pulselogo(vec2 p) {
        //  ivec2 ivec = textureSize(iChannel2, 0);
        //  p.x *= float(ivec.y)/ivec.x;
          p.x *= 258.0/534.0;
          p.y = -p.y;
          p*=1.0;
          p+=vec2(1.00, 1.00);
          p*=0.5;
          vec4 t = texture2D(iChannel2, p);
          float d = t.x + (1.0/256.0)*t.y + (1.0/(256.0*256.0))*t.z;
          return -2.0*d + 1.0;
        }

        vec2 hash (in vec2 p) {
          p = vec2 (dot (p, vec2 (127.1, 311.7)),
                    dot (p, vec2 (269.5, 183.3)));

          return -1. + 2.*fract (sin (p)*43758.5453123);
        }

        float noise (in vec2 p) {
          const float K1 = .366025404;
          const float K2 = .211324865;

          vec2 i = floor (p + (p.x + p.y)*K1);

          vec2 a = p - i + (i.x + i.y)*K2;
          vec2 o = step (a.yx, a.xy);
          vec2 b = a - o + K2;
          vec2 c = a - 1. + 2.*K2;

          vec3 h = max (.5 - vec3 (dot (a, a), dot (b, b), dot (c, c) ), .0);

          vec3 n = h*h*h*h*vec3 (dot (a, hash (i + .0)),
                                 dot (b, hash (i + o)),
                                 dot (c, hash (i + 1.)));

          return dot (n, vec3 (70.));
        }

        float fbm (in vec2 p, float time) {
          float c =  cos(time/sqrt(3.0));
          float d =  noise (p                 );
          d += .5*   noise (p + vec2(+c  ,+0.0));
          d += .25*  noise (p + vec2(+0.0,+c  ));
          d += .125* noise (p + vec2(-c  ,+0.0));
          d += .0625*noise (p + vec2(+0.0,-c  ));
          d /= (1. + .5 + .25 + .125 + .0625);
          return .5 + .5*d;
        }

        #define INTENSIFY(c) ((c) / max((c).x, max((c).y, (c).z)))

        const vec3 pulseCol1 = vec3(0xAF, 0x7D, 0x9F)/255.0;
        const vec3 pulseCol2 = vec3(0x77, 0x3A, 0x5F)/255.0;
        const vec3 lightningCol1 = INTENSIFY(pow(pulseCol1, vec3(5.0)));
        const vec3 lightningCol2 = INTENSIFY(pow(pulseCol1.yzx, vec3(5.0)));
        const vec3 lineCol = vec3(0.35);

        vec3 lightning(vec2 p, float d) {
          const float thickness = 0.25;
          const float haze = 2.5;
          const float size = .075;
          const int count = 3;

          vec3 col = vec3(0.0);

          float e1 = 1.6 + 0.4*sin(iTime*sqrt(2.0));
          float e2 = e1;

          float o = d;

          for (int i = 0; i < count; ++i) {
            float fi = float(i);
            float time = iTime + fi;
            float fe1 = (pow(fi + 1.0, 0.2))*e1;
            float fe2 = fe1;
            vec2 o1 = 1.5*time*vec2(0.0, -1);
            vec2 o2 = o1;

            vec2 fp1 = p + o1;
            vec2 fp2 = p + o2;
            float d1 = abs ((o*haze)*thickness / (o - fe1*fbm (fp1, time*0.11)));
            float d2 = abs ((o*haze)*thickness / (o - fe2*fbm (fp2, time*0.09)));
            col += d1*size*lightningCol1;
            col += d2*size*lightningCol2;
          }

          col /= float(count-1);

          return col;
        }

        float df(vec2 p, out int mat) {
          float c = length(1.0*p) - 2.25;
          p -= vec2(0.0, 0.5);
          mat = 0;
          float pt = pulsetop(p - vec2(-0.005, 0.43));
          float pb = pulsebottom(p - vec2(0, 0.0));
          float pl = pulselogo(p - vec2(0.0, -1.25));


          float d = min(-c, min(min(pt, pb), pl));

          if (d == pt) {
            mat = 1;
          } else if (d == pb) {
            mat = 0;
          } else if (d == pl) {
            mat = 2;
          } else if (d == -c) {
            mat = 0;
          }

          return d;
        }

        vec3 postProcess(in vec3 col, in vec2 q)  {
          col = clamp(col, vec3(0.0), vec3(1.0));
        //  col=pow(clamp(col,0.0,1.0),vec3(0.45));
        //  col=col*0.6+0.4*col*col*(3.0-2.0*col);  // contrast
        //  col=mix(col, vec3(dot(col, vec3(0.33))), -0.4);  // satuation
          col*=vec3(0.25+0.75*pow(19.0*q.x*q.y*(1.0-q.x)*(1.0-q.y),0.7));  // vigneting
          return col;
        }

        void mainImage(out vec4 fragColor, in vec2 fragCoord) {
          vec2 q = fragCoord/iResolution.xy;
          vec2 p = -1.0 + 2.0*q;
          p.x *= iResolution.x/iResolution.y;
          p*=1.75;

          int mat;
          float d = df(p, mat) + 0.1;
          float dd = d ;

          d*=150.0;
          vec3 col = vec3(0.0);

          if (mat == 0) {
            col += pulseCol2.zyx*smoothstep(3.0, 1.0, d);
            col -= vec3(0.1)*smoothstep(0.0, -80.0, d);
            col += lineCol*smoothstep(-3.0, -1.0, d)*smoothstep(3.0, 1.0, d);
          } else if (mat == 1) {
            col += pulseCol1.zyx*smoothstep(1.0, -1.0, d);
            col += vec3(0.55)*smoothstep(0.0, -80.0, d);
            col += lineCol*smoothstep(-3.0, -1.0, d)*smoothstep(3.0, 1.0, d);
          } else if (mat == 2) {
            col += lineCol*lineCol*smoothstep(-3.0, -1.0, d - 15.0)*smoothstep(3.0, 1.0, d - 15.0);
          }

          vec3 l = lightning(p, 1.65*(dd + 0.25));

          col += l;

          col = postProcess(col, q);

          col *= smoothstep(0.0, 5.0, iTime);

          fragColor = vec4(col, 1.0);


        }

    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <img id="image0" src="pulsetopdf.png" style="visibility: collapse"/>
    <img id="image1" src="pulsebottomdf.png" style="visibility: collapse"/>
    <img id="image2" src="pulselogodf.png" style="visibility: collapse"/>
    <canvas id="glcanvas" class="gl">
      Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>
  </body>
</html>