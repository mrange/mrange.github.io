<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>fsharp weekly</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;

      varying highp vec2 vTextureCoord;
      uniform sampler2D uSampler;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      // Created by mrange/2018
      // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.
      //

      #define TOLERANCE       0.0001
      #define MAX_RAY_LENGTH  16.0
      #define MAX_BOUNCES     3
      #define MAX_RAY_MARCHES 50

      #define PI              3.141592654
      #define TAU             (2.0*PI)

      #define DEG2RAD         (PI/180.0)

      #define AA              0

      void pR(inout vec2 p, float a)
      {
        p = cos(a)*p + sin(a)*vec2(p.y, -p.x);
      }

      float maxComp(in vec2 p)
      {
        return max(p.x,p.y);
      }

      float maxComp(in vec3 p)
      {
        return max(p.x,max(p.y,p.z));
      }

      float lengthN(in vec3 v, in float n)
      {
        v = abs(v);
        v = pow(v, vec3(n));
        return pow(v.x + v.y + v.z, 1.0/n);
      }

      float sdRoundCube(in vec3 p, float r)
      {
        return lengthN(p, 8.0) - r;
      }

      float sdBox(vec3 p, vec3 b)
      {
        vec3  di = abs(p) - b;
        float mc = maxComp(di);
        return min(mc,length(max(di,0.0)));
      }

      float sdSphere(vec3 p, float r)
      {
        return length(p) - r;
      }

      void pR90(inout vec2 p)
      {
        p = vec2(p.y, -p.x);
      }

      float sdCapsule(vec3 p, in vec2 h)
      {
        pR90(p.xy);
        float r = h.x;
        float c = h.y/2.0;
        return mix(length(p.xz) - r, length(vec3(p.x, abs(p.y) - c, p.z)) - r, step(c, abs(p.y)));
      }

      float sdCappedCapsule(vec3 p, in vec2 h)
      {
        pR90(p.xy);
        float r = h.x;
        float c = h.y/2.0;
        return mix(length(p.xz) - r, length(vec3(p.x, abs(p.y) - c, p.z)) - r, step(c, abs(p.y)));
      }

      float capsule(in vec3 p, in vec2 f, in vec2 t, float r)
      {
        vec2 diff = t - f;
        float a = atan(diff.y, diff.x);
        p.xy -= f + diff*0.5;
        pR(p.xy, a);

        return sdCapsule(p, vec2(r, length(diff)));
      }

      const float fsharpr  = 31.0/260.0;
      const float fsharpor = 0.02;

      float box2d(in vec2 p, in vec2 b)
      {
        vec2 d = abs(p) - b;
        return min(max(d.x,d.y),0.0) + length(max(d,0.0));
      }

      float manyCapsule(in vec3 p)
      {
        float s = 4.0*fsharpr;
        p.y = mod(p.y, s) - 0.5*s;
        return length(p.yz) - (fsharpr + fsharpor);
      }

      float manySphere(in vec3 p)
      {
        float sx = 4.0*fsharpr;
        float sy = 1.0;
        p.x = mod(p.x, sx) - 0.5*sx;
        p.y = mod(p.y, sy) - 0.5*sy;
        return length(p) - (fsharpr + fsharpor);
      }

      void pR45(inout vec2 p)
      {
        p = (p + vec2(p.y, -p.x))*sqrt(0.5);
      }

      float logoPound(in vec3 p)
      {
        float b1 = box2d(p.xy, vec2(0.5));
        float d1 = manyCapsule(p);
        float d2 = manySphere(p);
        pR90(p.xy);
        float d3 = manyCapsule(p);
        float d4 = manySphere(p);
        pR45(p.xy);
        float b2 = box2d(p.xy, vec2(0.7));

        return min(max(b1, min(d1, d3)), max(b2, min(d2, d4)));
      }

      float logoF(in vec3 p)
      {
        p -= vec3(-0.5, -2.0*fsharpr, -1.75*(1.0 - smoothstep(4.0, 5.0, iTime)));
        float d1 = capsule(p, vec2(0.5 - 2.0*fsharpr, 4.0*fsharpr), vec2(0.5 + 2.0*fsharpr, 4.0*fsharpr), fsharpr);
        float d2 = capsule(p, vec2(0.5 - 2.0*fsharpr, 4.0*fsharpr), vec2(0.5 - 2.0*fsharpr, -2.0*fsharpr), fsharpr);
        float d3 = capsule(p, vec2(0.5 - 2.0*fsharpr, 0.0), vec2(0.5, 0.0), fsharpr);
        return min(d1, min(d2, d3));
      }

      float linstep(in float f, in float t, in float x)
      {
        return clamp((x - f)/(t - f), 0.0, 1.0);
      }

      vec3 logoAbsorb()
      {
        float lt = (iTime - 4.75)*4.0;
        return 0.2*mix(
                  mix(10.0*vec3(3.0, 2.0, 1.0), vec3(1.0), linstep(0.0, 1.0, lt)),
                  vec3(3.0, 2.0, 1.0),
                  linstep(2.0, 12.0, lt));
      }

      vec3 logoRadiance()
      {
        float lt = (iTime - 4.75)*4.0;
        return mix(
                  mix(vec3(0.0), 0.5*vec3(0.3, 0.5, 1.0), linstep(0.0, 0.25, lt)),
                  (0.175 + 0.025*sin(iTime))*vec3(0.3, 0.5, 1.0),
                  linstep(1.5, 8.0, lt));
      }


      float fsharp(in vec3 p, out vec3 col, out float ref, out float trans, out vec3 absorb)
      {

        float drc = sdRoundCube(p, 1.0);

        pR45(p.xy);

        p.z -= -1.0;
        float dp = logoPound(p);

        float df = logoF(p);

        float d = drc;

        col    = vec3(0.1);
        ref    = 0.1;
        trans  = 0.0;

        absorb = logoAbsorb();

        d = max(d, -dp);
        if (d == -dp)
        {
          col    = vec3(0.9);
          ref    = 0.0;
        }

        if (iTime > 4.0)
        {
          d = min(d, df);
          if (d == df)
          {
            col    = vec3(0.3);
            ref    = 0.4;
            trans  = 0.9;
          }
        }

        return d;
      }

      void viewMod(inout vec3 p)
      {
        pR(p.xy, -PI/4.0);
        pR(p.xz, -0.4);
        pR(p.xy, PI/4.0);
        pR(p.xz, 1.5*PI*(1.0 - smoothstep(1.0, 3.0, iTime)));
      }

      float distanceField(in vec3 p, out vec3 col, out float ref, out float trans, out vec3 absorb)
      {
        viewMod(p);

        float i = fsharp(p, col, ref, trans, absorb);

        return i;
      }

      float distanceLogoF(in vec3 p)
      {
        viewMod(p);

        pR45(p.xy);

        p.z -= -1.0;

        return logoF(p);
      }



      vec3 saturate(in vec3 a)   { return clamp(a, 0.0, 1.0); }
      vec2 saturate(in vec2 a)   { return clamp(a, 0.0, 1.0); }
      float saturate(in float a) { return clamp(a, 0.0, 1.0); }

      const vec3 lightPos1 = 100.0*vec3(0.3, 0.0, 1.0);
      const vec3 lightPos2 = 100.0*vec3(0.5, -0.1, -1.2);

      const vec3 lightCol1 = vec3(8.0/8.0,7.0/8.0,6.0/8.0);
      const vec3 lightCol2 = vec3(8.0/8.0,6.0/8.0,7.0/8.0);
      const vec3 background = vec3(0.125);

      vec3 getSkyColor(vec3 rayDir)
      {
        pR(rayDir.xy, PI/2.0);
        pR(rayDir.yz, -PI/2.0);
        vec3 lightDir1 = normalize(lightPos1);
        vec3 lightDir2 = normalize(lightPos2);

        float ld1      = max(dot(lightDir1, rayDir), 0.0);
        float ld2      = max(dot(lightDir2, rayDir), 0.0);
        vec3 final     = background;

        if ((rayDir.y > abs(rayDir.x)*1.0) && (rayDir.y > abs(rayDir.z*0.25))) final = vec3(2.0)*rayDir.y;
        float roundBox = length(max(abs(rayDir.xz/max(0.0,rayDir.y))-vec2(0.9, 4.0),0.0))-0.1;
        final += vec3(0.8)* pow(saturate(1.0 - roundBox*0.5), 6.0);

        final += pow(lightCol1, vec3(2.0, 1.5, 1.5)) * pow(ld1, 8.0);
        final += lightCol1 * pow(ld1, 200.0);
        final += pow(lightCol2, vec3(2.0, 1.5, 1.5)) * pow(ld2, 8.0);
        final += lightCol2 * pow(ld2, 200.0);
        return final;
      }

      vec3 normal(in vec3 pos)
      {
        vec3 eps = vec3(.001,0.0,0.0);
        vec3 col;
        float ref;
        float trans;
        vec3 nor;
        vec3 absorb;
        nor.x = distanceField(pos+eps.xyy, col, ref, trans, absorb) - distanceField(pos-eps.xyy, col, ref, trans, absorb);
        nor.y = distanceField(pos+eps.yxy, col, ref, trans, absorb) - distanceField(pos-eps.yxy, col, ref, trans, absorb);
        nor.z = distanceField(pos+eps.yyx, col, ref, trans, absorb) - distanceField(pos-eps.yyx, col, ref, trans, absorb);
        return normalize(nor);
      }

      float rayMarch(in float dmod, in vec3 ro, inout vec3 rd, float mint, float minstep, out int rep, out vec3 col, out float ref, out float trans, out vec3 absorb)
      {
        float t = mint;
        for (int i = 0; i < MAX_RAY_MARCHES; i++)
        {
          float distance_ = distanceField(ro + rd*t, col, ref, trans, absorb);
          float distance = dmod*distance_;
          if (distance < TOLERANCE*t || t > MAX_RAY_LENGTH) break;
          t += max(distance, minstep);
          rep = i;
        }
        return t;
      }

      float softShadow(in vec3 pos, in vec3 ld, in float ll, float mint, float k)
      {
        const float minShadow = 0.25;
        float res = 1.0;
        float t = mint;
        vec3 col;
        float ref;
        float trans;
        vec3 absorb;
        for (int i=0; i<24; i++)
        {
          float distance = distanceField(pos + ld*t, col, ref, trans, absorb);
          res = min(res, k*distance/t);
          if (ll <= t) break;
          if(res <= minShadow) break;
          t += max(mint*0.2, distance);
        }
        return clamp(res,minShadow,1.0);
      }

      vec3 postProcess(in vec3 col, in vec2 q)
      {
        col=pow(clamp(col,0.0,1.0),vec3(0.75));
        col=col*0.6+0.4*col*col*(3.0-2.0*col);  // contrast
        col=mix(col, vec3(dot(col, vec3(0.33))), -0.4);  // satuation
        return col;
      }

      vec3 render(in vec3 ro, in vec3 rd)
      {
        vec3 lightPos = 2.5*vec3(-0.5, 0.25, -1.0);

        vec3 final  = vec3(0.0);

        vec3 ragg   = vec3(1.0);

        float tdist = 0.0;

        float refraction = 0.9;

        bool inside = false;

        float mint    = 0.01;
        float minstep = 0.001;

        for (int i = 0; i < MAX_BOUNCES; ++i)
        {
          if (maxComp(ragg) <  0.01) break;
          float dmod  = inside ? -1.0 : 1.0;
          vec3 absorb ;
          vec3 col    ;
          float ref   ;
          float trans ;
          int rep     ;
          float t     = rayMarch(dmod, ro, rd, mint, minstep, rep, col, ref, trans, absorb);
          tdist       += t;

          vec3 pos    = ro + t*rd;

          vec3 nor = vec3(0.0, 1.0, 0.0);

          if (t < MAX_RAY_LENGTH)
          {
            // Ray intersected object
            nor = normal(pos);
          }
          else if (i > 0)
          {
            // Bounce ray intersected sky
            final += ragg*getSkyColor(rd);
            break;
          }
          else
          {
            final = background;
            break;
          }

          float fresnel = pow(1.0 - abs(dot(nor, rd)), 2.0);

          ref = mix(ref, 1.0, fresnel);
          trans = mix(trans, 0.0, fresnel);
          trans += 0.001; // TODO: Why?

          float mref = refraction;

          if (inside)
          {
            nor = -nor;
            mref = 1.0/refraction;
          }

          vec3 refl = reflect(rd, nor);
          vec3 refr = refract(rd, nor, mref);

          vec3 lv   = lightPos - pos;
          vec3  ld  = normalize(lv);
          float ll  = length(lv);
          // TODO: Rework shadow to "work" with transparent objects
          float sha = 1.0;
          if (!inside)
          {
            sha = softShadow(pos, ld, ll, 0.01, 64.0);
          }

          float dif = max(dot(nor,ld),0.0);
          float occ = 1.0 - float(rep)/float(MAX_RAY_MARCHES);
          float l   = dif*sha*occ;

          vec3 lr   = vec3(0.0);
          float df  = distanceLogoF(pos) + fsharpr;
          if (!inside && df > TOLERANCE*10.0)
          {
            lr = logoRadiance() / df;
          }

          float lin = mix(0.2, 1.0, l);

          vec3 sky  = getSkyColor(refl);
          vec3 mcol = mix(lin*col + lr, sky, ref);

          vec3 beer = vec3(1.0);

          if (inside)
          {
            beer = exp(-absorb*t);
          }
          final      += (1.0 - trans)*ragg*beer*mcol;
          ragg       *= trans*beer;

          ro        = pos;

          if (refr == vec3(0.0))
          {
              rd = refl;
          }
          else
          {
            rd = refr;
            inside = !inside;
          }

        }


        return final;
      }

      vec3 getSample(in vec2 p)
      {
        // Clipping areas outside shape
        if(maxComp(abs(p)) > 1.0 && !(p.x > -1.2 && p.x < -1.0) && !(p.x > -1.7 && p.x < -1.0 && p.y > 0.25)) return background;
        vec3 ro  = vec3(0.0, 0.0, -3.0);
        ro.z += -6.0*(1.0 - smoothstep(0.0, 3.0, iTime));
        ro.x += 0.5*sin((iTime - 12.0)/4.0);
        ro.y += 0.1*sin((iTime - 12.0)*2.0/4.0);

        vec3 la  = vec3(0.0, 0.0, 1.0);

        vec3 ww = normalize(la - ro);
        vec3 uu = normalize(cross(vec3(0.0,1.0,0.0), ww ));
        vec3 vv = normalize(cross(ww,uu));
        vec3 rd = normalize( p.x*uu + p.y*vv + 2.0*ww );

        vec3 col = render(ro, rd);

        return col;
      }

      void mainImage(out vec4 fragColor, in vec2 fragCoord)
      {
        vec2 q=fragCoord.xy/iResolution.xy;
        vec2 p = -1.0 + 2.0*q;
        p.x *= iResolution.x/iResolution.y;

      #if AA == 0
        vec3 col = getSample(p);
      #elif AA == 1
        vec3 col  = vec3(0.0);
        vec2 unit = 1.0/iResolution.xy;
        for(int y = 0; y < 2; ++y)
        {
          for(int x = 0; x < 2; ++x)
          {
            col += getSample(p - 0.5*unit + unit*vec2(x, y));
          }
        }

        col /= 4.0;
      #endif

        float fadeIn   = smoothstep(0.0, 2.0, iTime);

        fragColor = vec4(postProcess(col, q)*fadeIn, 1.0);
      }
    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <div style="display:flex;justify-content:center;align-items:center;">
      <div>
        <h1>F# weekly</h1>
        <canvas id="glcanvas" width="1920" height="480">
          Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
        </canvas>
      </div>
    </div>
  </body>
</html>