<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>impulse-index</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            font-family: 'Josefin Slab';
            font-size: 5vh;
            color: white;
            background-color: black;
        }
        a:link {
          text-decoration: none;
          color: #FF8240;
        }

        a:visited {
          text-decoration: none;
          color: #FF8240;
        }

        a:hover {
          text-decoration: underline;
          color: #DFF180;
        }

        a:active {
          text-decoration: underline;
          color: #DFF180;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          top:0;
          left: 0;
          height: 100%;
          width: 100%;
          background-color: black;
        }
        div.footer {
          position: fixed;
          bottom: 0;
          width: 100%;
        }
        div.center-flex {
          display: flex;
          align-items: center;
          justify-content: center;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment"># version 100
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision highp float;

      uniform vec2 iResolution;
      uniform float iTime;

      varying highp vec2 vTextureCoord;
      uniform sampler2D uSampler;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------
      #define PI              3.141592654
      #define TAU             (2.0*PI)
      #define TIME            iTime
      #define RESOLUTION      iResolution
      #define SOFTMAX

      uniform float iOffset;

      float hash(in vec2 co) {
        return fract(sin(dot(co.xy ,vec2(12.9898,58.233))) * 13758.5453);
      }

      float psin(float a) {
        return 0.5 + 0.5*sin(a);
      }

      void rot(inout vec2 p, float a) {
        float c = cos(a);
        float s = sin(a);
        p = vec2(c*p.x + s*p.y, -s*p.x + c*p.y);
      }

      float circle(vec2 p, float r) {
        return length(p) - r;
      }

      float box(vec2 p, vec2 b, float r) {
        vec2 d = abs(p)-b + r;
        return length(max(d,0.0)) + min(max(d.x,d.y),0.0) - r;
      }

      float iteego(vec2 p) {
        const float s = 1.5 / 280.0;
        p /= s;
        p-= vec2(0.0, 60.0);
        float d0 = box(p, vec2(280.0, 280.0), 50.0);
        float d1 = box(p, vec2(196.0, 196.0), 20.0);
        float d2 = circle(p, 110.0);
        float d3 = circle(p, 66.0);
        float d4 = box(p - vec2(0.0, -280.0), vec2(88.0, 160.0), 20.0);
        float d5 = box(p - vec2(0.0, -280.0), vec2(44.0, 116.0), 10.0);
        float d = 1E6;
        d = min(d, d0);
        d = max(d, -d1);
        d = min(d, d2);
        d = max(d, -d3);
        d = min(d, d4);
        d = max(d, -d5);
        d *= s;
        return d;
      }

      float onoise(vec2 x) {
        x *= 0.5;
        float a = sin(x.x);
        float b = sin(x.y);
        float c = mix(a, b, psin(TAU/1.25*(a*b+a+b)));

        return c;
      }

      vec2 hextile(inout vec2 p) {
        const vec2 sz       = vec2(1.0, sqrt(3.0));
        const vec2 hsz      = 0.5*sz;

        // See Art of Code: Hexagonal Tiling Explained!
        // https://www.youtube.com/watch?v=VmrIDyYiJBA

        vec2 p1 = mod(p, sz)-hsz;
        vec2 p2 = mod(p - hsz*1.0, sz)-hsz;
        vec2 p3 = mix(p2, p1, vec2(length(p1) < length(p2)));
        vec2 n = p3 - p;
        p = p3;

        return n;
      }

      float falloff(float d, float w, float f, float a) {
        float dd = abs(d) - w;
        float b = 1.0/(f*f);
        float x = dd - f;
        float h = b*x*x*step(x,0.0);
        return mix(h, 1.0, float(dd < 0.0));
      }

      float pmin(float a, float b, float k) {
        float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
        return mix( b, a, h ) - k*h*(1.0-h);
      }

      float pmax(float a, float b, float k) {
        return -pmin(-a, -b, k);
      }

      float inoise(vec2 p) {
        p += 100.0;
      //  p = abs(p);
        p *= 0.125;
        vec2 n = hextile(p);
        float r = hash(n);
        const float s = 0.25 / 280.0;
        p /= s;
        p -= vec2(0.0, 60.0);
        rot(p, TAU*r);

        float d0 = box(p, vec2(238.0, 238.0), 00.0);
        float d1 = circle(p, 88.0);
        float d2 = box(p - vec2(0.0, -280.0), vec2(66.0, 138.0), 0.0);
        const float fo = 75.0+25.0;
        float h0 = falloff(d0, 21.0, fo, 1.0)*step(0.0, d2);
        float h1 = falloff(d1, 11.0, fo, 1.0);
        float h2 = falloff(d2, 11.0, fo, 1.0);

        float h = h0;
      #ifdef SOFTMAX
        const float sm = 0.5;
        h = pmax(h, h1, sm);
        h = pmax(h, h2, sm);
      #else
        h = max(h, h1);
        h = max(h, h2);
      #endif

      //  return max(h0, max(h1, h2));
        return 0.75*h;
      }


      float vnoise(vec2 x) {
        vec2 i = floor(x);
        vec2 w = fract(x);

      #if 1
        // quintic interpolation
        vec2 u = w*w*w*(w*(w*6.0-15.0)+10.0);
      #else
        // cubic interpolation
        vec2 u = w*w*(3.0-2.0*w);
      #endif

        float a = hash(i+vec2(0.0,0.0));
        float b = hash(i+vec2(1.0,0.0));
        float c = hash(i+vec2(0.0,1.0));
        float d = hash(i+vec2(1.0,1.0));

        float k0 =   a;
        float k1 =   b - a;
        float k2 =   c - a;
        float k3 =   d - c + a - b;

        float aa = mix(a, b, u.x);
        float bb = mix(c, d, u.x);
        float cc = mix(aa, bb, u.y);

        return k0 + k1*u.x + k2*u.y + k3*u.x*u.y;
      }

      float fbm(vec2 p) {
        const int mid = 3;
        const int mx = 7;
        const float aa = 0.42;
        const float pp = 2.25;
        const vec2 oo = -vec2(1.23, 1.5);
        const float rr = 1.2;

        vec2 op = p;

        float h = 0.0;
        float d = 0.0;
        float a = 1.0;

        for (int i = 0; i < mid; ++i) {
          h += a*onoise(p);
          d += (a);
          a *= aa;
          p += oo;
          p *= pp;
          rot(p, rr);
        }

        for (int i = mid; i < mx; ++i) {
          h += a*inoise(p);
          d += (a);
          a *= aa;
          p += oo;
          p *= pp;
          rot(p, rr);
        }

        return 0.5*mix(-1.0, 1.0, smoothstep(0.0, 1.2, (vnoise(0.50*op))))*(h/d);
      }

      float warp(vec2 p) {
        vec2 v = vec2(fbm(p), fbm(p+10.7*vec2(1.0, 1.0)));
        rot(v, 1.0+TIME*0.125);
        return mix(0., 1.0, v.x - v.y);
      }

      float height(vec2 p) {
        float a = 0.005*TIME;
        p += 5.0*vec2(cos(a), sin(sqrt(0.5)*a));
        p *= 2.0;
        p += 13.0;
        float h = warp(p);
        float rs = 3.0;
      //  return 0.5*tanh(rs*h)/rs;
        return 0.75*h;
      }

      vec3 normal(vec2 p) {
        // As suggested by IQ, thanks!
        vec2 eps = -vec2(2.0/RESOLUTION.y, 0.0);

        vec3 n;

        n.x = height(p + eps.xy) - height(p - eps.xy);
        n.y = 2.0*eps.x;
        n.z = height(p + eps.yx) - height(p - eps.yx);


        return normalize(n);
      }

      vec3 postProcess(vec3 col, vec2 q)  {
        col=pow(clamp(col,0.0,1.0),vec3(0.75));
        col=col*0.6+0.4*col*col*(3.0-2.0*col);
        col=mix(col, vec3(dot(col, vec3(0.33))), -0.4);
        col*=0.5+0.5*pow(19.0*q.x*q.y*(1.0-q.x)*(1.0-q.y),0.7);
        return col;
      }

      float df(vec2 p) {
        p *= 3.0;
        float d = iteego(p);
        return d;
      }

      void mainImage(out vec4 fragColor, in vec2 fragCoord) {
        vec2 q = fragCoord/RESOLUTION.xy;
        vec2 p = -1. + 2. * q;
        p.x *= RESOLUTION.x/RESOLUTION.y;

        const vec3 lp1 = vec3(0.8, -0.75, 0.8);
        const vec3 lp2 = vec3(-0., -1.5, -1.0);

        float aa = 10.0/RESOLUTION.y;

        vec3 col = vec3(0.0);
        float h = height(p);
        vec3 pp = vec3(p.x, h, p.y);
        vec3 ld1 = normalize(lp1 - pp);
        vec3 ld2 = normalize(lp2 - pp);

        float off = iOffset;

        vec2 bp = p;
        vec2 lp = p;
        bp.y += 0.5*off;
        lp.y += off;

        vec3 n = normal(bp);
        float diff1 = max(dot(ld1, n), 0.0);
        float diff2 = max(dot(ld2, n), 0.0);

        const vec3 baseCol1 = vec3(0.6, 0.8, 1.0);
        const vec3 baseCol2 = sqrt(baseCol1.zyx);

        col += baseCol1*pow(diff1, 16.0);
        col += 0.1*baseCol1*pow(diff1, 4.0);
        col += 0.15*baseCol2*pow(diff2,8.0);
        col += 0.015*baseCol2*pow(diff2, 2.0);

        col = clamp(col, 0.0, 1.0);
        col = mix(0.05*baseCol1, col, 1.0 - (1.0 - 0.5*diff1)*exp(- 2.0*smoothstep(-.1, 0.05, (h))));

        float d = df(lp);
        float bd = abs(d - 0.03) - 0.015;

        float shd = pow(psin(-0.25*TIME+(lp.x-lp.y)*1.5), 4.0);
        col += 1.0*baseCol1*(exp(-20.0*bd))*shd;
        col += 0.5*baseCol1*(exp(-1.0*bd))*shd;
        col -= 0.125*vec3(1.0)*(exp(-5.0*d));
        col = clamp(col, 0.0, 1.0);

        const float fadeIn = 6.0;
        col *= smoothstep(0.0, fadeIn*fadeIn, TIME*TIME);

        col = mix(col, vec3(1.0), smoothstep(0.0, aa, -d));

        col = postProcess(col, q);

        fragColor = vec4(col, 1.0);
      }
    </script>

    <script id="shader-vs" type="x-shader/x-vertex"># version 100
      attribute highp vec2 pos;
      attribute highp vec2 texPos;

      varying highp vec2 vTextureCoord;

      void main(void) {
        gl_Position=vec4(pos,0.0,1.0);
        vTextureCoord=texPos;
      }
    </script>
  </head>

  <body onload="init()">
    <div style="height: 85vh">&nbsp;</div>
    <div class="center-flex">
      <table style="width: 65vw" >
        <tr>
          <td rowspan="5" style="transform: rotate(-90deg);" >ITEEGO</td>
          <td>&nbsp;</td>
          <td><h1>Welcome</h1></td>
        </tr>
        <tr>
          <td>&nbsp;</td>
          <td>&nbsp;</td>
        </tr>
        <tr>
          <td>&nbsp;</td>
          <td>Iteego has specialized in bridging the gap between new software and actual business requirements since 2002. We are experts in minimizing cost and maximizing returns in large-scale e-commerce implementation and enterprise software integration.</td>
        </tr>
        <tr>
          <td>&nbsp;</td>
          <td>&nbsp;</td>
        </tr>
        <tr>
          <td>&nbsp;</td>
          <td>We pride ourselves in being a leading provider of continuous delivery in large-scale application development and we deliver results in productivity that has yet to be matched in the industry.</td>
        </tr>
      </table>
    </div>
    <div class="center-flex">
      <div>
        <canvas id="glcanvas" class="gl">
          Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
        </canvas>
      </div>
    </div>
  </body>
</html>