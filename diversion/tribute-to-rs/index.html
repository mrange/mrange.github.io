<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>Our tribute to Red Sector</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="demo-system-v2.js" type="text/javascript"></script>
    <script src="tiny-sdf.js" type="module"></script>
    <link rel="preconnect" href="https://fonts.googleapis.com">
    <link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
    <link href="https://fonts.googleapis.com/css2?family=Josefin+Slab&display=swap" rel="stylesheet">
    <style>
        body {
            font-family: 'Josefin Slab';
            font-size: 2vw;
            color: white;
            background-color: black;
        }
        a:link {
          text-decoration: none;
          color: #FF8240;
        }
        a:visited {
          text-decoration: none;
          color: #FF8240;
        }
        a:hover {
          text-decoration: underline;
          color: #DFF180;
        }
        a:active {
          text-decoration: underline;
          color: #DFF180;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          top:0;
          left: 0;
          height: 100%;
          width: 100%;
          background-color: black;
        }
        div.center-flex {
          display: flex;
          align-items: center;
          justify-content: center;
        }
    </style>

    <script id="vs_default" type="x-shader/x-vertex">
      precision highp float;

      in vec4 a_position;
      in vec3 a_normal  ;
      in vec2 a_texcoord;

      out vec2 v_texcoord;

      void main(void) {
        gl_Position = a_position;
        v_texcoord  = a_texcoord;
      }
    </script>

    <script id="fs_red" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------

      void main(void) {
        vec3 col = vec3(1.0, 0.0, 0.0);

        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_moody" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------

      uniform sampler2D prev_frame    ;
      uniform sampler2D image  ;

      uniform sampler2D texts  ;

      const float TextHeight = 1.0/8.0;

      vec3 imageCol(vec3 col, vec2 p) {
        p *= 0.5;
        vec2 ap = abs(p);
        vec2 sp = step(ap, vec2(0.5));
        p += 0.5;
        p.y = 1.0-p.y;
        vec4 tcol = texture(image, p);
        col = mix(col, tcol.xyz*tcol.xyz, tcol.w*sp.x*sp.y);
        return col;
      }


      float textsT(vec2 p, float iz, float n) {
        p *= iz;
        p *= 0.5;
        if (abs(p.y) > 0.5*TextHeight) {
          return 0.0;
        }
        p += 0.5;
        p.y -= TextHeight*0.5;
        p.y -= (n-4.0)*TextHeight;
        p.y = 1.0-p.y;
        vec4 tcol = texture(texts, p);

        float off = 0.033*iz;
        return smoothstep(0.75-off, 0.75+off, tcol.x);
      }

      const mat2 brot = ROT(2.399);
      //  simplyfied version of Dave Hoskins blur
      vec3 dblur(vec2 q,float rad) {
        vec3 acc=vec3(0);
        const float m = 0.002;
        vec2 pixel=vec2(m*resolution.y/resolution.x,m);
        vec2 angle=vec2(0,rad);
        rad=1.;
        const int iter = 24;
        for (int j=0; j<iter; ++j) {
          rad += 1./rad;
          angle*=brot;
          vec4 col=texture(prev_frame,q+pixel*(rad-1.)*angle);
          col.xyz*=col.xyz;
          acc+=col.xyz;
        }
        return acc*(1.0/float(iter));
      }

      void main(void) {
        const float botz  = 2.0;
        const float top   = (1.0-4.0*TextHeight);
        const float bot   = (-1.0+2.0*botz*TextHeight*1.25);
        const float mid   = (top+bot)*0.5;

        vec2 q = v_texcoord;
        vec2 p = -1.0+2.0*q;
        vec2 pp = p;
        float r = RESOLUTION.x/RESOLUTION.y;
        p.x *= r;
        vec2 zp = 0.95*p;
        zp.x /= r;
        vec2 zq = 0.5+0.5*zp;
        vec3 col = vec3(0.0);
        col = imageCol(col, p);
        float ftm = TAU*TIME*0.5;
        float flash = smoothstep(-0.5, 0.5, sin(ftm+p.x)*sin(sqrt(0.5)*ftm+p.y)*sin(sqrt(3.0)*ftm+p.x+p.y));
        col = min(col, mix(0.33, 1.0, flash*step(mid, p.y)));
        vec2 sp = 40.0*p+TAU*TIME/5.0;
        float rad = 2.0*mix(0.5, 1.5, 0.5+0.5*sin(sp.x)*sin(sp.y));

        float fade = smoothstep(top*1.1, top, p.y)*smoothstep(1.125*bot, bot, p.y);

        vec3 bcol = dblur(zq, rad);
        bcol = tanh(2.0*bcol);
        if (p.y > mid) {
          bcol *= bcol.z;
          bcol *= 2.0/3.0*vec3(0.25, 0.5, 1.0);
        } else {
          bcol *= bcol.x;
          bcol *= 2.0/3.0*vec3(1.0, 0.75, 0.5);
        }
        bcol *= fade;
        col += bcol;
        col *= smoothstep(1.5, 0.5, length(pp));

        vec2 p0 = p;
        p0.y -= -1.0+1.25*TextHeight*botz;
        float t0 = textsT(p0, 1.0/botz, 1.0);

        vec2 p1 = p;
        p1.y -= 1.0-TextHeight;
        float t1 = textsT(p1, 1.0, 2.0);

        vec2 p2 = p;
        p2.y -= 1.0-TextHeight*3.0;
        float t2 = textsT(p2, 1.0, 3.0);

        float fi = smoothstep(6.0, 14.0, TIME+4.0*p.y-4.0-2.0*p.x*p.x);
        const vec3 lcol = vec3(2.0/3.0);

        col = mix(col, lcol, fi*t0);
        col = mix(col, lcol, fi*t1);
        col = mix(col, lcol, fi*t2);
        col *= smoothstep(bstart, bstart-1.0, TIME);
        col *= smoothstep(0.0, 6.0, TIME);
        col = sqrt(col);
        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_intro" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------

      uniform sampler2D prev_frame  ;
      uniform sampler2D almost_mono ;
      uniform sampler2D red_sector  ;
      uniform sampler2D texts       ;

      const float TextHeight = 1.0/8.0;

      float textsT(vec2 p, float iz, float n) {
        p *= iz;
        p *= 0.5;
        if (abs(p.y) > 0.5*TextHeight) {
          return (0.0);
        }
        p += 0.5;
        p.y -= TextHeight*0.5;
        p.y -= (n-4.0)*TextHeight;
        p.y = 1.0-p.y;
        vec4 tcol = texture(texts, p);

        float off = 0.033*iz;
        return smoothstep(0.75-off, 0.75+off, tcol.x);
      }

      vec3 clampedCol(sampler2D sampler, vec3 col, vec2 p) {
        p *= 0.5;
        vec2 ap = abs(p);
        vec2 sp = step(ap, vec2(0.5));
        p += 0.5;
        p.y = 1.0-p.y;
        vec4 tcol = texture(sampler, p);
        col = mix(col, tcol.xyz*tcol.xyz, tcol.w*sp.x*sp.y);
        return col;
      }

      const float btime0    = BTIME(0.0);
      const float btime1    = BTIME(1.0);
      const float btime1_nxt= BTIME(1.5);
      const float btime1_fo = BTIME(1.75);
      const float btime2    = BTIME(2.0);
      const float btime2_fi = BTIME(2.5);
      const float btime3    = BTIME(3.0);

      vec3 effect0(vec3 col, vec2 p, float ratio) {
        vec2 p0 = p;
        float t0 = textsT(p0, 1.0/ratio, mix(0.0, 4.0, step(btime1_nxt, TIME)));
        vec2 p1 = p;
        p1 /= ratio+0.0125*(TIME-btime0);
        p1.y -= -0.3-0.005*(TIME-btime0);
        col = clampedCol(almost_mono, col, p1);
        col = mix(col, vec3(1.0), t0*step(btime1, TIME));

        vec2 p2 = p;
        p2.y -= -0.05;
        float d3 = abs(p2.y)+abs(p2.x*0.125);
        const vec3 gcol = vec3(1.0, 0.71, 0.5).xzy;
        float gf = 0.0;
        gf += step(btime1, TIME)*smoothstep(btime1+bperiod/4.0, btime1,TIME);
        gf += step(btime1_nxt, TIME)*smoothstep(btime1_nxt+bperiod/4.0, btime1_nxt,TIME);
        col += (gcol/3.0)/max(d3, 0.025)*gf;
        vec2 p4 = p;
        p4.y -= 0.85;
        col *= smoothstep(btime0, btime1, TIME-length(p4));
        col *= smoothstep(btime2, btime1_fo, TIME);
        return col;
      }

      // License: Unknown, author: Martijn Steinrucken, found: https://www.youtube.com/watch?v=VmrIDyYiJBA
      vec2 hextile(inout vec2 p) {
        // See Art of Code: Hexagonal Tiling Explained!
        // https://www.youtube.com/watch?v=VmrIDyYiJBA
        const vec2 sz       = vec2(1.0, sqrt(3.0));
        const vec2 hsz      = 0.5*sz;

        vec2 p1 = mod(p, sz)-hsz;
        vec2 p2 = mod(p - hsz, sz)-hsz;
        vec2 p3 = dot(p1, p1) < dot(p2, p2) ? p1 : p2;
        vec2 n = ((p3 - p + hsz)/sz);
        p = p3;

        n -= vec2(0.5);
        // Rounding to make hextile 0,0 well behaved
        return round(n*2.0)*0.5;
      }

      vec3 effect1(vec3 col, vec2 p, float ratio) {
        float aa = 4.0/RESOLUTION.y;
        vec2 p0 = p;
        float fade = smoothstep(btime2, btime2_fi, TIME-0.5*length(p-vec2(0.0, 1.0)));
        float z0 = mix(0.5, aa, sqrt(fade));
        p0 /= z0;
        vec2 n0 = hextile(p0);
        vec2 c0 = n0*z0*vec2(-1.0, -sqrt(3.0));
        float d0 = length(p-c0) - 0.25*z0;
        float d1 = length(p0) - 0.125;
        d1 *= z0;
        vec2 p1 = p;
        col = mix(vec3(0.0), clampedCol(red_sector, col, c0), fade);
        return col;
      }

      void main(void) {
        vec2 q = v_texcoord;
        vec2 p = -1.0+2.0*q;
        float ratio = RESOLUTION.x/RESOLUTION.y;
        p.x *= ratio;

        vec3 col = vec3(0.0);
        if (TIME > btime2) {
          col = effect1(col, p, ratio);
        } else {
          col = effect0(col, p, ratio);
        }
        col = sqrt(col);
        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_beatbox" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------


      #define TOLERANCE       0.0001
      #define MAX_RAY_LENGTH  24.0
      #define MAX_RAY_MARCHES 70
      #define NORM_OFF        0.001

      //#define DF0
      //#define FLAIR
      //#define BOXY

      const float Nop   = 0.0;
      const float Drum  = 1.0;
      const float Hey   = -1.0;

      const float[] track4 = float[64*3]
      (
        Drum  // 0
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop   // 8
      , Nop
      , Drum
      , Nop
      , Nop
      , Nop
      , Drum
      , Nop
      , Drum  // 16
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop // 24
      , Nop
      , Drum
      , Nop
      , Nop
      , Nop
      , Drum
      , Nop
      , Nop // 32
      , Nop
      , Drum
      , Nop
      , Drum
      , Nop
      , Nop
      , Nop
      , Nop // 40
      , Nop
      , Drum
      , Nop
      , Nop
      , Nop
      , Drum
      , Nop
      , Drum // 48
      , Nop
      , Nop
      , Nop
      , Nop
      , Nop
      , Drum
      , Nop
      , Nop // 56
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Drum // 0 - break
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 8
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Drum // 16
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 24
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Nop // 32
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 40
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Drum // 48
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Drum
      , Drum
      , Nop // 56
      , Drum
      , Drum
      , Nop
      , Hey
      , Nop
      , Hey
      , Nop
      , Drum // 0 -break
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 8
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Drum // 16
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 24
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Nop // 32
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      , Nop // 40
      , Nop
      , Drum
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Drum // 48
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Drum
      , Nop
      , Drum // 56
      , Nop
      , Nop
      , Nop
      , Hey
      , Nop
      , Nop
      , Nop
      );

      float funkyBeat(float time) {
        const float btime3 = BTIME(3.0);
        const float btime9 = BTIME(9.0);
        const float diff   = btime9-btime3;
        const float len    = float(track4.length());
        const float beat   = diff/len;

        float tm = time-btime3;
        float bt = 1.0-mod(tm, beat)/beat;
        float idx = floor(tm/beat);
        if (idx >= 0.0 && idx < len) {
          float effect = track4[int(idx)];
          return effect*(bt);
        } else {
          return Nop;
        }
      }

      float g_funkyBeat0;
      float g_funkyBeat1;

      // License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
      const vec4 hsv2rgb_K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
      vec3 hsv2rgb(vec3 c) {
        vec3 p = abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www);
        return c.z * mix(hsv2rgb_K.xxx, clamp(p - hsv2rgb_K.xxx, 0.0, 1.0), c.y);
      }
      // License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
      //  Macro version of above to enable compile-time constants
      #define HSV2RGB(c)  (c.z * mix(hsv2rgb_K.xxx, clamp(abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www) - hsv2rgb_K.xxx, 0.0, 1.0), c.y))

      const float hoff      = 0.;
      const vec3 skyCol     = HSV2RGB(vec3(hoff+0.75, 0.70, 0.25));
      const vec3 glowCol0   = HSV2RGB(vec3(hoff+0.025, 0.85, 0.00125));
      const vec3 glowCol1   = HSV2RGB(vec3(hoff+0.95, 0.85, 0.05));
      const vec3 sunCol1    = HSV2RGB(vec3(hoff+0.55, 0.50, 0.5));
      const vec3 sunCol2    = HSV2RGB(vec3(hoff+0.95, 0.75, 25.0));
      const vec3 diffCol    = HSV2RGB(vec3(hoff+0.40, 0.75, 0.125));
      const vec3 sunDir1    = normalize(vec3(3., 3.0, -7.0));

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      #define ROTY(a)               \
        mat3(                       \
          +cos(a) , 0.0 , +sin(a)   \
        , 0.0     , 1.0 , 0.0       \
        , -sin(a) , 0.0 , +cos(a)   \
        )

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      #define ROTZ(a)               \
        mat3(                       \
          +cos(a) , +sin(a) , 0.0   \
        , -sin(a) , +cos(a) , 0.0   \
        , 0.0     , 0.0     , 1.0   \
        )

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      #define ROTX(a)               \
        mat3(                       \
          1.0 , 0.0     , 0.0       \
        , 0.0 , +cos(a) , +sin(a)   \
        , 0.0 , -sin(a) , +cos(a)   \
        )


      float torus( vec3 p, vec2 t ) {
        vec2 q = vec2(length(p.xz)-t.x,p.y);
        return length(q)-t.y;
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float ref(inout vec3 p, vec3 r) {
        float d = dot(p, r);
        p -= r*min(0.0, d)*2.0;
        return d < 0.0 ? 0.0 : 1.0;
      }

      #define SCA(a)      vec2(sin(a), cos(a))

      // License: MIT, author: Inigo Quilez, found: https://iquilezles.org/articles/distfunctions/
      float rayPlane(vec3 ro, vec3 rd, vec4 p) {
        return -(dot(ro,p.xyz)+p.w)/dot(rd,p.xyz);
      }

      // License: MIT, author: Inigo Quilez, found: https://iquilezles.org/www/articles/distfunctions2d/distfunctions2d.htm
      float box(vec2 p, vec2 b) {
        vec2 d = abs(p)-b;
        return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
      }

      float box(vec3 p, vec3 b) {
        vec3 q = abs(p) - b;
        return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0);
      }

      float boxf(vec3 p, vec3 b, float e) {
             p = abs(p  )-b;
        vec3 q = abs(p+e)-e;
        return min(min(
            length(max(vec3(p.x,q.y,q.z),0.0))+min(max(p.x,max(q.y,q.z)),0.0),
            length(max(vec3(q.x,p.y,q.z),0.0))+min(max(q.x,max(p.y,q.z)),0.0)),
            length(max(vec3(q.x,q.y,p.z),0.0))+min(max(q.x,max(q.y,p.z)),0.0));
      }

      // License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
      float pmin(float a, float b, float k) {
        float h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0);
        return mix(b, a, h) - k*h*(1.0-h);
      }

      float pmax(float a, float b, float k) {
        return -pmin(-a, -b, k);
      }

      // License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
      vec3 pmin(vec3 a, vec3 b, float k) {
        vec3 h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0);
        return mix(b, a, h) - k*h*(1.0-h);
      }


      vec3 pabs(vec3 a, float k) {
        return -pmin(a, -a, k);
      }

      float sphere(vec3 p, float r) {
        return length(p) - r;
      }

      float sphere4(vec3 p, float r) {
        p *= p;
        return pow(dot(p, p), 0.25) - r;
      }

      float sphere8(vec3 p, float r) {
        p *= p;
        p *= p;
        return pow(dot(p, p), 0.125) - r;
      }

      // License: MIT OR CC-BY-NC-4.0, author: mercury, found: https://mercury.sexy/hg_sdf/
      vec3 mod3(inout vec3 p, vec3 size) {
        vec3 c = floor((p + size*0.5)/size);
        p = mod(p + size*0.5,size) - size*0.5;
        return c;
      }

      float bbox(vec3 p, vec3 b) {
        vec3 q = abs(p) - b;
        return (max(q.x,max(q.y,q.z)));
      }

      float dfbase(vec3 p, out float ogd) {
        float sz = mix(3.0, 3.25, g_funkyBeat0*g_funkyBeat0);
        vec3 p0 = p;
        float d0 = box(p0, vec3(sz));
        vec3 p1 = p;
        float d1 = boxf(p1, vec3(sz+0.01), 0.)-0.01;

        float d = d0;
        d = min(d, d1);

        ogd = d1;

        return d;
      }

      #if defined(DF0)
      float dfeffect(vec3 p, out float ogd) {
        return dfbase(p, ogd);
      }
      #elif defined(DF1)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      float dfeffect(vec3 p, out float ogd) {
      #if defined(BOXY)
        float d0 = box(p, vec3(3.0));
        float d1 = boxf(p, vec3(2.98), 0.)-0.01;
      #else
        float d0 = sphere8(p, (3.));
      #endif
        vec3 p2 = p;
        const vec3 rot0 = vec3(0.0, 1.0, 0.0)*ROTZ(-PI/4.0);
        const vec3 rot1 = vec3(0.0, 1.0, 0.0)*ROTX(PI/4.0);
        p2 = abs(p2);
        ref(p2, rot0);
        ref(p2, rot1);
        p2.y -= 3.0;
        float d2 = torus(p2, vec2(1.5, 0.025));

        vec3 p3 = p2;
        const float r3 = 0.75;
        p3.y -= r3;
        float d3 = length(p3) - r3;

        vec3 p4 = p2;
        p4.y -= 0.05;
        float d4 = length(p4)-0.05;

        float gd = d2;
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif
        gd = min(gd, d4);
        gd = abs(gd);


        float d = d0;

        d = pmax(d, -(gd-0.00), 0.1);
      #if defined(BOXY)
        d = min(d, d1);
      #endif
        d = min(d, d2);
        d = pmax(d, -d3, 1.);
        d = min(d, d4);

        ogd = gd;

        return d;
      }
      #elif defined(DF2)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      float dfeffect(vec3 p, out float ogd) {
      #if defined(BOXY)
        float d0 = box(p, vec3(3.0));
        float d1 = boxf(p, vec3(3.01), 0.0)-0.01;
      #else
        float d0 = sphere8(p, (3.));
      #endif

        float d3 = length(p)-(3.-0.12);

        float d = d0;
        d = pmax(d, -d3, 1.0);
        d = min(d, d3);
      #if defined(BOXY)
        d = min(d, d1);
      #endif

        float d2 = max(max(d, d3-0.4), -(d3-0.25));

        float gd = d2;
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif
        gd = abs(gd);

        ogd = gd;


        return d;

      }
      #elif defined(DF3)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      float dfeffect(vec3 p, out float ogd) {
        float sz = mix(3.0, 3.25, g_funkyBeat0*g_funkyBeat0);
      #if defined(BOXY)
        float d0 = box(p, vec3(sz));
        float d1 = boxf(p, vec3(sz+0.01), 0.0)-0.01;
      #else
        float d0 = sphere8(p, (sz));
      #endif
        vec3 p2 = p;
      #if defined(FLAIR)
        const float bsz = 6.0/(3.-1.0);
      #else
        const float bsz = 6.0/(24.0-1.0);
      #endif
        mod3(p2, vec3(bsz));
        float d2 = box(p2, vec3(0.80*bsz*0.5))-0.15*bsz*0.5;

        float d4 = sphere4(p, sz+mix(-0.005, 0.005, sqrt(g_funkyBeat0)));

        float d = d2;
        d = max(d, d0);
      #if defined(BOXY)
        d = min(d, d1);
      #endif
        d = min(d, d4);

        float gd = d4;
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif
        ogd = gd;

        return d;

      }
      #elif defined(DF4)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      #define ZOOM        (1.)
      #define FWD(x)      exp2((x)*ZOOM)
      #define REV(x)      (log2(x)/ZOOM)

      float dfeffect(vec3 p, out float ogd) {
        float sz = mix(3.0, 3.25, g_funkyBeat0*g_funkyBeat0);
      #if defined(BOXY)

        float d0 = box(p, vec3(sz));
        float d1 = boxf(p, vec3(sz+0.01), 0.0)-0.01;
      #else
        float d0 = sphere8(p, (sz));
      #endif
        float d3 = sphere4(p, sz);
        float d4 = min(min(abs(p.x), abs(p.y)), abs(p.z))-0.015;
      #if defined(FLAIR)
        float d5 = max(d0, d4);
      #endif
        vec3 p2 = p;

        p2 = abs(p2);
        p2 -= 3.0;

        vec3 fp2 = FWD(abs(p2));

        float n = floor(max(max(fp2.x, fp2.y), fp2.z));

        float x0 = REV(n);
        float x1 = REV(n+1.0);

        float m = (x0+x1)*0.5;
        float w = x1-x0;

        float d2 = abs(bbox(p2, vec3(m)))-(w*0.5)+0.015;

        d0 = max(d0, d2);

        float d = d0;
        d = min(d, d3);
      #if defined(FLAIR)
        d = min(d, d5);
      #endif
      #if defined(BOXY)
        d = min(d, d1);
      #endif

        float gd = d3;
      #if defined(FLAIR)
        gd = min(gd, d5);
      #endif
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif
        ogd = gd;

        return d;

      }

      #elif defined(DF5)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      #define ZOOM        (1.-.1)
      #define FWD(x)      exp2((x)*ZOOM)
      #define REV(x)      (log2(x)/ZOOM)

      float dfeffect(vec3 p, out float ogd) {
      #if defined(BOXY)
        float d0 = box(p, vec3(3.0));
        float d1 = boxf(p, vec3(3.01), 0.0)-0.01;
      #else
        float d0 = sphere8(p, (3.));
      #endif
        float d3 = sphere4(p, 3.0-.001);

        vec3 p2 = p;
        p2 = abs(p2);
        p2 -= 3.0;
        float fp2 = FWD(length(p2));

        float n = floor(fp2);

        float x0 = REV(n);
        float x1 = REV(n+1.0);

        float m = (x0+x1)*0.5;
        float w = x1-x0;

        float d2 = abs(length(p2)-m)-(w*0.5)+0.025;
      #if defined(FLAIR)
        float d5 = abs(length(p2)-m)-0.005;
        d5 = max(d0, d5);
      #endif

        d0 = max(d0, d2);

        float d = d0;
        d = min(d, d3);
      #if defined(BOXY)
        d = min(d, d1);
      #endif

        float gd = d3;
      #if defined(FLAIR)
        gd = min(gd, d5);
      #endif
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif

        ogd = gd;

        return d;

      }

      #elif defined(DF6)
      #define BACKSTEP


      const float fixed_radius2 = 1.9;
      const float min_radius2 = 0.1;
      const float folding_limit = 1.0;
      const float scale = -2.4;

      void sphere_fold(inout vec3 z, inout float dz) {
        float r2 = dot(z, z);
        if(r2 < min_radius2) {
          float temp = (fixed_radius2 / min_radius2);
          z *= temp;
          dz *= temp;
        } else if(r2 < fixed_radius2) {
          float temp = (fixed_radius2 / r2);
          z *= temp;
          dz *= temp;
        }
      }

      void box_fold(inout vec3 z, inout float dz) {
        z = clamp(z, -folding_limit, folding_limit) * 2.0 - z;
      }

      float mb(vec3 z, out float ddd) {
        vec3 offset = z;
        float dr = 1.0;
      #if defined(FLAIR)
        float d2 = 1E3;
      #else
        float d2 = sphere4(z,mix(2.1, 2.3, g_funkyBeat0));
      #endif

        for(int n = 0; n < 5; ++n) {
          box_fold(z, dr);
          sphere_fold(z, dr);

          z = scale * z + offset;
          dr = dr * abs(scale) + 1.5;
      #if defined(FLAIR)
          if (n < 2) {
            float d = (length(z))/abs(dr)-0.06;
            d2 = min(d, d2);
          }
      #endif
        }

        float d = (length(z))/abs(dr)-0.04;
      #if !defined(FLAIR)
        d2 = pmax(d2, -d, 0.5);
      #endif
        d = min(d, d2);

        ddd = d2;

        return d;
      }


      float dfeffect(vec3 p, out float ogd) {
        const float z = 1.5;
        vec3 p0 = p/z;
        float d2;
        float d0 = mb(p0, d2);
        d0 *= z;
        float d1 = d2*z;

        ogd = d1;

        float d = d0;
        d = min(d, d1);
        return d0;

      }
      #elif defined(DF7)
      #if !defined(BOXY)
      #define BACKSTEP
      #endif

      #define ZOOM        (0.2025)
      #define REV(x)      exp2((x)*ZOOM)
      #define FWD(x)      (log2(x)/ZOOM)

      float dfeffect(vec3 p, out float ogd) {
      #if defined(BOXY)
        float d0 = box(p, vec3(3.0));
        float d1 = boxf(p, vec3(3.01), 0.0)-0.01;
      #else
        float d0 = sphere8(p, (3.));
      #endif
        float d3 = sphere4(p, 3.0-.001);

        vec3 p4 = p;
        p4 = abs(p4);
        p4 -= 3.0-0.75;

        float d4 = sphere(p4, 0.75);

        vec3 p2 = p;
        p2 = abs(p2);
        p2 -= 3.0;
        float fp2 = FWD(length(p2));

        float n = floor(fp2);

        float x0 = REV(n);
        float x1 = REV(n+1.0);

        float m = (x0+x1)*0.5;
        float w = x1-x0;

        float d2 = abs(length(p2)-m)-(w*0.5)+0.025;
      #if defined(FLAIR)
        float d5 = abs(length(p2)-m)-0.005;
      #endif
        d0 = max(d0, d2);

        float d = d0;
        d = min(d, d3);
      #if defined(BOXY)
        d = min(d, d1);
      #endif
        d = pmax(d, -d4, 0.5);
        d = min(d, d4);
        float gd = d3;
      #if defined(FLAIR)
        gd = min(gd, max(d5, d0));
      #endif
      #if defined(BOXY)
        gd = min(gd, d1);
      #endif
        gd = pmax(gd, -d4, 0.5);
        ogd = gd;

        return d;

      }
      #else
      float dfeffect(vec3 p, out float ogd) {
        return dfbase(p, ogd);
      }
      #endif

      mat3 g_rot;
      float g_gd;

      //#define FADER

      float df(vec3 p) {
        p *= g_rot;
        float gd1;
        float d1 = dfeffect(p, gd1);

      #if defined(FADER)
        float gd0;
        float d0 = dfbase(p, gd0);

        float m = fader;
        float d = mix(d1, d0, m);
        float gd = mix(gd1, gd0, m);
      #else
        float d = d1;
        float gd = gd1;
      #endif
        g_gd = min(g_gd, gd);

        return d;
      }


      vec3 normal(vec3 pos) {
        vec2  eps = vec2(NORM_OFF,0.0);
        vec3 nor;
        nor.x = df(pos+eps.xyy) - df(pos-eps.xyy);
        nor.y = df(pos+eps.yxy) - df(pos-eps.yxy);
        nor.z = df(pos+eps.yyx) - df(pos-eps.yyx);
        return normalize(nor);
      }

      float rayMarch(vec3 ro, vec3 rd, out int iter) {
        float t = 0.0;
        const float tol = TOLERANCE;
        vec2 dti = vec2(1e10,0.0);
        int i = 0;
        for (i = 0; i < MAX_RAY_MARCHES; ++i) {
          float d = df(ro + rd*t);
          if (d<dti.x) { dti=vec2(d,t); }
          if (d < TOLERANCE || t > MAX_RAY_LENGTH) {
            break;
          }
          t += d;
        }
      #if defined(BACKSTEP)
        if(i==MAX_RAY_MARCHES) { t=dti.y; };
      #endif
        iter = i;
        return t;
      }

      vec3 render0(vec3 ro, vec3 rd) {
        vec3 col = vec3(0.0);
        float sd = max(dot(sunDir1, rd), 0.0);
        float sf = 1.0001-sd;

        float funkyBeat0 = mix(1.0, 8.0, g_funkyBeat0);
        float funkyBeat1 = mix(1.0, 2.0, g_funkyBeat1);
        float funkyBeat2 = mix(1.0, 1.25, g_funkyBeat1);
        col += funkyBeat0*clamp(vec3(1.0/abs(rd.y))*glowCol0, 0.0, 1.0);

        col += 0.75*skyCol*pow((1.0-abs(rd.y)), 8.0);
        col += 2.0*sunCol1*pow(sd, 100.0);
        col += sunCol2*pow(sd, 800.0);

        float tp1  = rayPlane(ro, rd, vec4(vec3(0.0, -1.0, 0.0), -6.0));

        if (tp1 > 0.0) {
          vec3 pos  = ro + tp1*rd;
          vec2 pp = pos.xz;
          float db = box(pp, funkyBeat2*vec2(5.0, 9.0))-3.0;
          vec3 pcol = vec3(0.0);
          pcol += vec3(4.0)*skyCol*rd.y*rd.y*smoothstep(0.25, 0.0, db);
          pcol += vec3(0.8)*skyCol*exp(-0.5*max(db, 0.0));
          pcol += 0.25*sqrt(skyCol)*max(-db, 0.0);
          col += pcol*funkyBeat1;
        }

        return clamp(col, 0.0, 10.0);
      }

      vec3 render1(vec3 ro, vec3 rd, vec2 sp) {

        int iter;
        g_gd = 1E3;
        float t = rayMarch(ro, rd, iter);
        vec3 ggcol = (glowCol1)*inversesqrt(max(g_gd, 0.00025));
        vec3 col = render0(ro, rd);

        vec3 p = ro+rd*t;
        vec3 n = normal(p);
        vec3 r = reflect(rd, n);
        float fre0 = 1.0+dot(rd, n);
        float fre = fre0;
        fre *= fre;
        float dif = dot(sunDir1, n);

        float ao = 1.0-float(iter)/float(MAX_RAY_MARCHES);
        float fo = mix(0.2, 1.0, ao);
        if (t < MAX_RAY_LENGTH) {
          col = vec3(0.0);
          col += sunCol1*dif*dif*diffCol*fo*1.0;
          col += mix(0.33, 1.0, fre)*render0(p, r)*fo*0.75;
        }

      //  col *= smoothstep(0.1, -0.1, cos((TAU*TIME-2.0*sp.y)/30.0));
      //  col *= 0.0;
        col += clamp(ggcol, 0.0, 4.0);
        return col;
      }
      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      mat3 rotX(float a) {
        float c = cos(a);
        float s = sin(a);
        return mat3(
          1.0 , 0.0 , 0.0
        , 0.0 , +c  , +s
        , 0.0 , -s  , +c
        );
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      mat3 rotY(float a) {
        float c = cos(a);
        float s = sin(a);
        return mat3(
          +c  , 0.0 , +s
        , 0.0 , 1.0 , 0.0
        , -s  , 0.0 , +c
        );
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      mat3 rotZ(float a) {
        float c = cos(a);
        float s = sin(a);
        return mat3(
          +c  , +s  , 0.0
        , -s  , +c  , 0.0
        , 0.0 , 0.0 , 1.0
        );
      }

      float bounce(float t, float dy, float dropOff) {
        const float g = 10.0;
        float p0 = 2.0*dy/g;

        t += p0/2.0;

        float ldo = log(dropOff);
        float yy = 1.0 - (1.0 - dropOff) * t / p0;


        if (yy > 1e-4) {
          float n  = floor(log(yy) / ldo);
          float dn = pow(dropOff, n);

          float yyy = dy * dn;
          t -= p0 * (1.0 - dn) / (1.0 - dropOff);

          return -0.5*g*t*t + yyy*t;

        }
        else {
            return 0.0;
        }
      }

      // License: Unknown, author: Matt Taylor (https://github.com/64), found: https://64.github.io/tonemapping/
      vec3 aces_approx(vec3 v) {
        v = max(v, 0.0);
        v *= 0.6f;
        float a = 2.51f;
        float b = 0.03f;
        float c = 2.43f;
        float d = 0.59f;
        float e = 0.14f;
        return clamp((v*(a*v+b))/(v*(c*v+d)+e), 0.0f, 1.0f);
      }


      vec3 effect(vec2 p, vec2 pp) {
        float funkyBeat =funkyBeat(TIME);
        g_funkyBeat0 = max(0.0, funkyBeat);
        g_funkyBeat1 = max(0.0, -funkyBeat);
        float tm  = 0.5*TIME+10.0;
        g_rot = rotX(0.333*tm)*rotZ(0.5*tm)*rotY(0.23*tm);

        vec3 ro = 1.75*vec3(5.0, 1., 0.);
        ro.xz *= ROT(-0.1*tm);
        const vec3 la = vec3(0.0, 0.0, 0.0);
        const vec3 up = normalize(vec3(0.0, 1.0, 0.0));

        vec3 ww = normalize(la - ro);
        vec3 uu = normalize(cross(up, ww ));
        vec3 vv = (cross(ww,uu));
        const float fov = tan(TAU/6.);
        vec3 rd = normalize(-p.x*uu + p.y*vv + fov*ww);

        vec3 col = render1(ro, rd, p);

        col -= 0.05*length(pp);
        col = aces_approx(col);

        col = max(col,0.0);
        col = sqrt(col);

        return col;
      }

      void main() {
        vec2 q = v_texcoord;
        vec2 p = -1. + 2. * q;
        vec2 pp = p;
        p.x *= RESOLUTION.x/RESOLUTION.y;
        vec3 col = vec3(0.0);
        col = effect(p, pp);
        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_dawn" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------


      #define LAYERS            5.0

      #define PI                3.141592654
      #define TAU               (2.0*PI)
      #define TIME              time
      #define RESOLUTION        resolution
      #define ROT(a)            mat2(cos(a), sin(a), -sin(a), cos(a))
      #define SCA(a)          vec2(sin(a), cos(a))

      const float btime9 = BTIME(9.0);
      const float btime11 = BTIME(11.0);
      const float btime_fo = BTIME(13.0-0.125);
      const float btime13 = BTIME(13.0);

      const vec2 impulse_sca0 = SCA(0.0);

      // License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
      float pmin(float a, float b, float k) {
        float h = clamp(0.5+0.5*(b-a)/k, 0.0, 1.0);
        return mix(b, a, h) - k*h*(1.0-h);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float pmax(float a, float b, float k) {
        return -pmin(-a, -b, k);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float circle(vec2 p, float r) {
        return length(p) - r;
      }

      // License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
      float box(vec2 p, vec2 b) {
        vec2 d = abs(p)-b;
        return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
      }

      // License: MIT, author: Inigo Quilez, found: https://www.iquilezles.org/www/articles/smin/smin.htm
      float horseshoe(vec2 p, vec2 c, float r, vec2 w) {
        p.x = abs(p.x);
        float l = length(p);
        p = mat2(-c.x, c.y,
                  c.y, c.x)*p;
        p = vec2((p.y>0.0)?p.x:l*sign(-c.x),
                  (p.x>0.0)?p.y:l );
        p = vec2(p.x,abs(p.y-r))-w;
        return length(max(p,0.0)) + min(0.0,max(p.x,p.y));
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_e(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 1.05+off;
        p -= vec2(0.5, 0.5);
        // TODO: Optimize
        return min(box(p, vec2(0.4, 0.1)), max(circle(p, 0.5), -circle(p, 0.3)));
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_I(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 0.25+off;
        p -= vec2(0.125, 0.75);
        return box(p, vec2(0.125, 0.75));
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_l(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 0.2+off;
        p -= vec2(0.10, 0.5);
        return box(p, vec2(0.1, 0.666));
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_m(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 2.2+off;
        p -= vec2(1.1, 0.5);
        p.y = -p.y;
        p.x = abs(p.x);
        p -= vec2(0.5, 0.0);
        float d = horseshoe(p, impulse_sca0, 0.5, vec2(0.5, 0.1));
        return d;
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_n(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 1.15+off;
        p -= vec2(0.55, 0.5);
        p.y = -p.y;
        float l = horseshoe(p, impulse_sca0, 0.5, vec2(0.5, 0.1));
        return l;
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_p(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 1.05+off;
        p -= vec2(0.55, 0.5);
        float b = box(p - vec2(-0.45, -0.25), vec2(0.1, 0.75));
        float c = abs(circle(p, 0.4)) - 0.1;
        return min(b, c);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_r(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 0.6+off;
        p -= vec2(0.1, 0.5);
        float d0 = box(p-vec2(0.20, 0.4), vec2(0.3, 0.1));
        float d1 = box(p, vec2(0.1, 0.5));
        return min(d0, d1);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_s(inout vec2 pp, float off) {
        const mat2 rots1 = ROT(-PI/6.0-PI/2.0);
        const mat2 rots2 = ROT(PI);
        vec2 p = pp;
        pp.x -= 0.875+off;
        p -= vec2(0.435, 0.5);
        p *= rots1;
        float u = horseshoe(p - vec2(-0.25*3.0/4.0, -0.125/2.0), impulse_sca0, 0.375, vec2(0.2, 0.1));
        p *= rots2;
        float l = horseshoe(p - vec2(-0.25*3.0/4.0, -0.125/2.0), impulse_sca0, 0.375, vec2(0.2, 0.1));
        return min(u,l);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_u(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 1.2+off;
        p -= vec2(0.6, 0.475);
        return horseshoe(p - vec2(0.0, 0.125), impulse_sca0, 0.5, vec2(0.4, 0.1));
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_t(inout vec2 pp, float off) {
        vec2 p = pp;
        pp.x -= 0.6+off;
        p -= vec2(0.3, 0.6);
        float d0 = box(p-vec2(0.0, 0.3), vec2(0.3, 0.1));
        float d1 = box(p, vec2(0.1, 0.6));
        return min(d0, d1);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse(vec2 p, float off) {
        p += vec2(3.385+3.0*off, 0.5);

        float d = 1E6;
        d = min(d, impulse_I(p, off));
        d = min(d, impulse_m(p, off));
        d = min(d, impulse_p(p, off));
        d = min(d, impulse_u(p, off));
        d = min(d, impulse_l(p, off));
        d = min(d, impulse_s(p, off));
        d = min(d, impulse_e(p, off));

        return d;
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float presents(vec2 p, float off) {
        p += vec2(3.65+3.5*off, 0.5);

        float d = 1E6;
        d = min(d, impulse_p(p, off));
        d = min(d, impulse_r(p, off));
        d = min(d, impulse_e(p, off));
        d = min(d, impulse_s(p, off));
        d = min(d, impulse_e(p, off));
        d = min(d, impulse_n(p, off));
        d = min(d, impulse_t(p, off));
        d = min(d, impulse_s(p, off));

        return d;
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse_bars(vec2 p, float d) {
        float db = abs(abs(p.y)-0.1)-0.05;
        return pmax(d, -db, 0.025);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float impulse(vec2 p) {
        float d = impulse(p, 0.25);
        return impulse_bars(p, d);
      }

      // License: CC0, author: Mårten Rånge, found: https://github.com/mrange/glsl-snippets
      float presents(vec2 p) {
        float d = presents(p, 0.25);
        return impulse_bars(p, d);
      }


      // License: Unknown, author: nmz (twitter: @stormoid), found: https://www.shadertoy.com/view/NdfyRM
      float sRGB(float t) { return mix(1.055*pow(t, 1./2.4) - 0.055, 12.92*t, step(t, 0.0031308)); }
      // License: Unknown, author: nmz (twitter: @stormoid), found: https://www.shadertoy.com/view/NdfyRM
      vec3 sRGB(in vec3 c) { return vec3 (sRGB(c.x), sRGB(c.y), sRGB(c.z)); }

      // License: Unknown, author: Matt Taylor (https://github.com/64), found: https://64.github.io/tonemapping/
      vec3 aces_approx(vec3 v) {
        v = max(v, 0.0);
        v *= 0.6f;
        float a = 2.51f;
        float b = 0.03f;
        float c = 2.43f;
        float d = 0.59f;
        float e = 0.14f;
        return clamp((v*(a*v+b))/(v*(c*v+d)+e), 0.0f, 1.0f);
      }

      // License: Unknown, author: Unknown, found: don't remember
      float tanh_approx(float x) {
        //  Found this somewhere on the interwebs
        //  return tanh(x);
        float x2 = x*x;
        return clamp(x*(27.0 + x2)/(27.0+9.0*x2), -1.0, 1.0);
      }

      // License: WTFPL, author: sam hocevar, found: https://stackoverflow.com/a/17897228/418488
      const vec4 hsv2rgb_K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
      vec3 hsv2rgb(vec3 c) {
        vec3 p = abs(fract(c.xxx + hsv2rgb_K.xyz) * 6.0 - hsv2rgb_K.www);
        return c.z * mix(hsv2rgb_K.xxx, clamp(p - hsv2rgb_K.xxx, 0.0, 1.0), c.y);
      }

      // License: MIT OR CC-BY-NC-4.0, author: mercury, found: https://mercury.sexy/hg_sdf/
      vec2 mod2(inout vec2 p, vec2 size) {
        vec2 c = floor((p + size*0.5)/size);
        p = mod(p + size*0.5,size) - size*0.5;
        return c;
      }

      // License: Unknown, author: Unknown, found: don't remember
      float hash(float co) {
        return fract(sin(co*12.9898) * 13758.5453);
      }

      // License: Unknown, author: Unknown, found: don't remember
      vec2 hash2(vec2 p) {
        p = vec2(dot (p, vec2 (127.1, 311.7)), dot (p, vec2 (269.5, 183.3)));
        return fract(sin(p)*43758.5453123);
      }

      vec2 shash2(vec2 p) {
        return -1.0+2.0*hash2(p);
      }

      vec3 toSpherical(vec3 p) {
        float r   = length(p);
        float t   = acos(p.z/r);
        float ph  = atan(p.y, p.x);
        return vec3(r, t, ph);
      }


      // License: CC BY-NC-SA 3.0, author: Stephane Cuillerdier - Aiekick/2015 (twitter:@aiekick), found: https://www.shadertoy.com/view/Mt3GW2
      vec3 blackbody(float Temp) {
        vec3 col = vec3(255.);
        col.x = 56100000. * pow(Temp,(-3. / 2.)) + 148.;
        col.y = 100.04 * log(Temp) - 623.6;
        if (Temp > 6500.) col.y = 35200000. * pow(Temp,(-3. / 2.)) + 184.;
        col.z = 194.18 * log(Temp) - 1448.6;
        col = clamp(col, 0., 255.)/255.;
        if (Temp < 1000.) col *= Temp/1000.;
        col *= col;
        return col;
      }


      // License: MIT, author: Inigo Quilez, found: https://www.shadertoy.com/view/XslGRr
      float noise(vec2 p) {
        // Found at https://www.shadertoy.com/view/sdlXWX
        // Which then redirected to IQ shader
        vec2 i = floor(p);
        vec2 f = fract(p);
        vec2 u = f*f*(3.-2.*f);

        float n =
               mix( mix( dot(shash2(i + vec2(0.,0.) ), f - vec2(0.,0.)),
                         dot(shash2(i + vec2(1.,0.) ), f - vec2(1.,0.)), u.x),
                    mix( dot(shash2(i + vec2(0.,1.) ), f - vec2(0.,1.)),
                         dot(shash2(i + vec2(1.,1.) ), f - vec2(1.,1.)), u.x), u.y);

        return 2.0*n;
      }

      float fbm(vec2 p, float o, float s, int iters) {
        p *= s;
        p += o;

        const float aa = 0.5;
        const mat2 pp = 2.04*ROT(1.0);

        float h = 0.0;
        float a = 1.0;
        float d = 0.0;
        for (int i = 0; i < iters; ++i) {
          d += a;
          h += a*noise(p);
          p += vec2(10.7, 8.3);
          p *= pp;
          a *= aa;
        }
        h /= d;

        return h;
      }

      float height(vec2 p) {
        float h = fbm(p, 0.0, 5.0, 5);
        h *= 0.3;
        h += 0.0;
        return (h);
      }

      vec2 raySphere(vec3 ro, vec3 rd, vec4 sph) {
        vec3 oc = ro - sph.xyz;
        float b = dot( oc, rd );
        float c = dot( oc, oc ) - sph.w*sph.w;
        float h = b*b - c;
        if( h<0.0 ) return vec2(-1.0);
        h = sqrt( h );
        return vec2(-b - h, -b + h);
      }

      vec3 stars(vec3 ro, vec3 rd, vec2 sp, float hh) {
        vec3 col = vec3(0.0);

        const float m = LAYERS;
        hh = tanh_approx(20.0*hh);

        for (float i = 0.0; i < m; ++i) {
          vec2 pp = sp+0.5*i;
          float s = i/(m-1.0);
          vec2 dim  = vec2(mix(0.05, 0.003, s)*PI);
          vec2 np = mod2(pp, dim);
          vec2 h = hash2(np+123.4+i);
          vec2 o = -1.0+2.0*h;
          float y = sin(sp.x);
          pp += o*dim*0.5;
          pp.y *= y;
          float l = length(pp);

          float h0 = fract(h.x);
          float h1 = fract(h0*1377.0);
          float h2 = fract(h0*1887.0);
          float h3 = fract(h0*2997.0);
          float h4 = fract(h0*8997.0);

          vec3 scol = mix(8.0*h2, 0.25*h2*h2, s)*blackbody(mix(4000.0, 30000.0, h1*h1));

          const float a = 0.1;
          float b = mix(0.0, 1.0, smoothstep(-0.9, 1.0, sin(h4*TAU+TAU*(TIME)*0.25)));
          vec3 ccol = col + b*exp(-(mix(6000.0, 2000.0, hh)/mix(2.0, 0.25, s))*max(l-0.001, 0.0))*scol;
          col = h3 < y ? ccol : col;
        }

        return col;
      }

      vec3 sky(vec3 ro, vec3 rd, vec2 sp, vec3 lp, out float cf) {
        vec2 ssp = toSpherical(rd.zyx).yz;
        float ld = max(dot(normalize(lp-ro), rd),0.0);
        float y = -0.5+sp.x/PI;
        y = max(abs(y)-0.02, 0.0)+0.1*smoothstep(0.5, PI, abs(sp.y));
        vec3 blue = hsv2rgb(vec3(0.6, 0.75, 0.35*exp(-15.0*y)));
        float fi = smoothstep(btime11, btime13, TIME);
        float ci = pow(ld, 10.0)*2.0*exp(-25.0*(y-0.2*fi*fi));
        vec3 yellow = blackbody(2000.0)*ci;
        float ci2 = pow(ld, 4.0)*2.0*exp(-mix(1.0, 2.0, smoothstep(-1.0, 0., sin(TAU*24.0*(abs(ssp.y)+0.25*(sp.y))))));
        vec3 spokes = 2.0*vec3(1.0, 0., 0.25)*ci2;
        cf = ci;
        return blue+(yellow+spokes)*fi;
      }

      vec4 moon(vec3 ro, vec3 rd, vec2 sp, vec3 lp, vec4 md) {
        vec2 mi = raySphere(ro, rd, md);

        vec3 p    = ro + mi.x*rd;
        vec3 n    = normalize(p-md.xyz);
        vec3 r    = reflect(rd, n);
        vec3 ld   = normalize(lp - p);
        float fre = dot(n, rd)+1.0;
        fre = pow(fre, 15.0);
        float dif = max(dot(ld, n), 0.0);
        float spe = pow(max(dot(ld, r), 0.0), 8.0);
        float i = tanh_approx(20.0*fre*spe+0.05*dif);
        vec3 col = hsv2rgb(vec3(0.6, mix(0.66, 0.0, tanh_approx(3.0*i)), i));

        float t = tanh_approx(0.25*(mi.y-mi.x));

        return vec4(vec3(col), t);
      }


      vec3 galaxy(vec3 ro, vec3 rd, vec2 sp, out float sf) {
        vec2 gp = sp;
        gp *= ROT(0.67);
        gp += vec2(-1.0, 0.5);
        float h1 = height(2.0*sp);
        float gcc = dot(gp, gp);
        float gcx = exp(-(abs(3.0*(gp.x))));
        float gcy = exp(-abs(10.0*(gp.y)));
        float gh = gcy*gcx;
        float cf = smoothstep(0.05, -0.2, -h1);
        vec3 col = vec3(0.0);
        col += blackbody(mix(300.0, 3000.0, gcx*gcy))*gcy*gcx;
        col += hsv2rgb(vec3(0.6, 0.5, 0.00125/gcc));
        col *= mix(mix(0.15, 1.0, gcy*gcx), 1.0, cf);
        sf = gh*cf;
        return col;
      }

      vec3 impulseCol(vec3 col, vec2 p) {
        float r = RESOLUTION.x/RESOLUTION.y;
        float aa = 4.0/RESOLUTION.y;
        float iz = 4.5/r;
      //  p.y += -0.5;
        float d = impulse(p*iz)/iz;

        float w = 0.008/iz;
        float od = abs(d-2.0*w)-w;
        col = mix(col, mix(col, vec3(0.0), 0.25), smoothstep(0.0, -aa, d));
        col = mix(col, mix(col, vec3(1.0), 1.0), smoothstep(0.0, -aa, od));
        float gd = p.y;
        gd *= iz;
        gd *= gd;
        return col;
      }

      float segmentx(vec2 p) {
        float d0 = abs(p.y);
        float d1 = length(p);
        return p.x > 0.0 ? d0 : d1;
      }

      vec3 meteorite(vec3 ro, vec3 rd, vec2 sp) {
        const float period = 6.0;
        float mtime = mod(TIME, period);
        float ntime = floor(TIME/period);
        float h0 = hash(ntime);
        float h1 = fract(1667.0*h0);
        vec2 mp = sp;
        mp.x += -1.0;
        mp.y += -0.5*h1;
        mp *= ROT(PI+mix(-PI/4.0, PI/4.0, h0));
        float m = smoothstep(0.0, 0.25, mtime/period);
        mp.x += mix(-1.0, 2.0, m);

        float d0 = length(mp);
        float d1 = segmentx(mp);

        vec3 col = vec3(0.0);

        col += exp(-mix(300.0, 600.0, smoothstep(-0.5, 0.5, sin(10.0*TAU*TIME)))*max(d0, 0.0));
        col += 0.5*exp(-4.0*max(d0, 0.0))*exp(-1000.0*max(d1, 0.0));

        col *= vec3(1.0)*smoothstep(0.2, 0.8, sp.x);

        return col;
      }

      vec3 color(vec2 p, vec3 ro, vec3 rd, vec3 lp, vec4 md) {
        vec2 sp = toSpherical(rd.xzy).yz;

        float sf = 0.0;
        float cf = 0.0;
        vec3 col = vec3(0.0);

        vec4 mcol = moon(ro, rd, sp, lp, md);
        vec3 scol = sky(ro, rd, sp, lp, cf);

        col += stars(ro, rd, sp, sf)*(1.0-tanh_approx(10.0*cf));
        col += galaxy(ro, rd, sp, sf);
        col = mix(col, mcol.xyz, mcol.w);
        col += scol;
        col += meteorite(ro, rd, sp);


        return col;
      }

      vec3 effect(vec2 p) {

        vec3 ro = vec3(0.0, 0.0, 0.0);
        vec3 lp = 500.0*vec3(1.0, -0.25, 0.0);
        vec4 md = 50.0*vec4(vec3(1.0, 1., -0.6), 0.5);
        vec3 la = vec3(1.0, 0.5, 0.0);
        vec3 up = vec3(0.0, 1.0, 0.0);
        float tdiff = btime13-btime9;
        float a = mix(-0.8, -0.0, (TIME-btime9)/tdiff);
      //  a = 0.0;
        la.xy *= ROT(a);

        vec3 ww = normalize(la - ro);
        vec3 uu = normalize(cross(up, ww));
        vec3 vv = (cross(ww,uu));
        vec3 rd = normalize(p.x*uu + p.y*vv + 2.0*ww);
        vec3 col= color(p, ro, rd, lp, md);
      //  col -= 0.01*vec3(0.0, 1.0, 2.0).zyx;

        col *= smoothstep(btime9+1.0, btime9+6.0, TIME);
        col = impulseCol(col, p);
        col *= smoothstep(btime9, btime9+1.0, TIME);
        col = aces_approx(col);
        col += smoothstep(btime_fo, btime13, TIME);
        col = sRGB(col);
        return col;
      }

      void main() {
        vec2 q = v_texcoord;
        vec2 p = -1.0 + 2.0*q;
        p.x *= RESOLUTION.x/RESOLUTION.y;

        vec3 col = effect(p);

        frag_color = vec4(col,1.0);
      }

    </script>

    <script id="fs_romeo_knight" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------

      uniform sampler2D image  ;
      uniform sampler2D texts  ;

      const float TextHeight = 1.0/8.0;

      float textsT(vec2 p, float iz, float n) {
        p *= iz;
        p *= 0.5;
        if (abs(p.y) > 0.5*TextHeight) {
          return 0.0;
        }
        p += 0.5;
        p.y -= TextHeight*0.5;
        p.y -= (n-4.0)*TextHeight;
        p.y = 1.0-p.y;
        vec4 tcol = texture(texts, p);

        float off = 0.033*iz;
        return smoothstep(0.75-off, 0.75+off, tcol.x);
      }

      vec3 imageCol(vec3 col, vec2 p) {
        p *= 0.5;
        vec2 ap = abs(p);
        vec2 sp = step(ap, vec2(0.5));
        p += 0.5;
        p.y = 1.0-p.y;
        vec4 tcol = texture(image, p);
        col = mix(col, tcol.xyz*tcol.xyz, tcol.w*sp.x*sp.y);
        return col;
      }

      float romeoT(vec2 p, float iz) {
        p.x -= 0.5/iz;
        float t = textsT(p, iz, 5.0);
        t *= step(p.x, 0.);
        return t;
      }

      float knightT(vec2 p, float iz) {
        p.x += 0.5/iz;
        float t = textsT(p, iz, 5.0);
        t *= step(0., p.x);
        return t;
      }
      const float btime13  = BTIME(13.0);
      const float btime_fi = BTIME(13.125);
      const float btime_b0 = BTIME(13.0);
      const float btime_e0 = BTIME(13.25);
      const float btime_b1 = BTIME(13.125);
      const float btime_e1 = BTIME(13.5);
      const float btime_fo = BTIME(14.5);
      const float btime15 = BTIME(15.0);
      vec3 effect(vec2 p) {
        float fi0 = smoothstep(btime_b0, btime_e0, TIME);
        float fi1 = smoothstep(btime_b1, btime_e1, TIME);
        vec3 col = vec3(0.0);
        float ratio = RESOLUTION.x/RESOLUTION.y;
        vec2 p0 = p;
        col = imageCol(col, p0/ratio);
        col *= 0.25;
        vec2 p1 = p;
        col = imageCol(col, p1);

        float tm = TIME-btime_fo+p.y-0.5;
        tm *= 4.0;
        float np = mod(floor(p.y/0.01), 2.0)-0.5;
        p.x += 0.25*sign(np)*exp(-tm*tm);
        vec2 p2 = p;
        p2 -= vec2(-0.5, 0.);
        float t2 = romeoT(p2, mix(0.05, 0.33, fi0));
        vec2 p3 = p;
        p3 -= vec2(0.5, -0.66);
        float t3 = knightT(p3, mix(0.05, 0.33, fi1));
        col = mix(col, mix(col, vec3(1.0, 0.0, 0.), fi0), t2);
        col = mix(col, mix(col, vec3(1.0, 1.0, 1.0), fi1), t3);
        col += smoothstep(btime_fi, btime13, TIME);

        col = sqrt(col);
        return col;
      }

      void main() {
        vec2 q = v_texcoord;
        vec2 p = -1. + 2. * q;
        p.x *= RESOLUTION.x/RESOLUTION.y;
        vec3 col = effect(p);
        frag_color = vec4(col, 1.0);
      }
    </script>

    <script id="fs_solo" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------------
      // PRELUDE
      // -----------------------------------------------------------------------------
      precision highp float;

      uniform float     time              ;
      uniform vec2      resolution        ;
      uniform sampler2D frequency_data    ;
      uniform sampler2D time_domain_data  ;

      in vec2 v_texcoord  ;

      out vec4 frag_color ;

      #define TIME        time
      #define RESOLUTION  resolution

      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))

      const float bstart = 25.61;
      const float bpm    = 125.0;
      const float bhz    = bpm/60.0;
      const float bperiod= 8.0/bhz;

      #define BTIME(n) ((n)*bperiod+bstart)

      float beatCount(float tm) {
        return (tm-bstart)*bhz;
      }

      float beat(float tm) {
        return smoothstep(0.25, 1.0, cos(TAU*beatCount(tm)));
      }

      // -----------------------------------------------------------------------------
      // SHADER
      // -----------------------------------------------------------------------------


      #define PI          3.141592654
      #define TAU         (2.0*PI)
      #define L2(x)       dot(x, x)
      #define ROT(a)      mat2(cos(a), sin(a), -sin(a), cos(a))
      #define TTIME       (TAU*TIME)
      #define PSIN(x)     (0.5+0.5*sin(x))

      const float corner0 = 0.02;
      const float corner1 = 0.075;
      const float topy    = 0.0475+corner0*0.5;

      float hash(vec2 co) {
        return fract(sin(dot(co, vec2(12.9898,58.233))) * 13758.5453);
      }

      // See Art of Code: Hexagonal Tiling Explained!
      // https://www.youtube.com/watch?v=VmrIDyYiJBA
      vec2 hextile(inout vec2 p) {

        const vec2 sz       = vec2(1.0, sqrt(3.0));
        const vec2 hsz      = 0.5*sz;

        vec2 p1 = mod(p, sz)-hsz;
        vec2 p2 = mod(p - hsz, sz)-hsz;
        vec2 p3 = mix(p2, p1, vec2(dot(p1, p1) < dot(p2, p2)));
        vec2 n = ((p3 - p + hsz)/sz);
        p = p3;

        // Rounding to make hextile 0,0 well behaved
        return round(n*2.0)/2.0;
      }

      float tanh_approx(float x) {
      //  return tanh(x);
        float x2 = x*x;
        return clamp(x*(27.0 + x2)/(27.0+9.0*x2), -1.0, 1.0);
      }

      float vmax(vec2 v) {
        return max(v.x, v.y);
      }

      // http://mercury.sexy/hg_sdf/
      float corner(vec2 p) {
        return length(max(p, vec2(0))) + vmax(min(p, vec2(0)));
      }


      // Distance fields by IQ
      // https://iquilezles.org/articles/distfunctions2d
      float roundedBox(vec2 p, vec2 b, vec4 r) {
          r.xy = (p.x>0.0)?r.xy : r.zw;
          r.x  = (p.y>0.0)?r.x  : r.y;
          vec2 q = abs(p)-b+r.x;
          return min(max(q.x,q.y),0.0) + length(max(q,0.0)) - r.x;
      }

      float box(vec2 p, vec2 b) {
          vec2 d = abs(p)-b;
          return length(max(d,0.0)) + min(max(d.x,d.y),0.0);
      }

      float parallelogram(vec2 p, float wi, float he, float sk) {
          vec2 e = vec2(sk,he);
          p = (p.y<0.0)?-p:p;
          vec2  w = p - e; w.x -= clamp(w.x,-wi,wi);
          vec2  d = vec2(dot(w,w), -w.y);
          float s = p.x*e.y - p.y*e.x;
          p = (s<0.0)?-p:p;
          vec2  v = p - vec2(wi,0); v -= e*clamp(dot(v,e)/dot(e,e),-1.0,1.0);
          d = min( d, vec2(dot(v,v), wi*he-abs(s)));
          return sqrt(d.x)*sign(-d.y);
      }

      float circle(vec2 p, float r) {
        return length(p)-r;
      }

      vec2 refl(vec2 p, vec2 n) {
        p -= n*min(0.0, dot(p, n))*2.0;
        return p;
      }

      // http://mercury.sexy/hg_sdf/
      float mod1(inout float p, float size) {
        float halfsize = size*0.5;
        float c = floor((p + halfsize)/size);
        p = mod(p + halfsize, size) - halfsize;
        return c;
      }

      // Smooth min by IQ
      // https://iquilezles.org/articles/smin
      float pmin(float a, float b, float k) {
          float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
          return mix( b, a, h ) - k*h*(1.0-h);
      }

      float pmax(float a, float b, float k) {
        return -pmin(-a, -b, k);
      }

      float pabs(float a, float k) {
        return pmax(a, -a, k);
      }

      float omega(vec2 p) {
        const float lw = 0.07;
        const float sm = 0.066;
        p.x = abs(p.x);
        vec2 p0 = p;
        p0.y -= 0.075;
        float d0 = circle(p0, 0.36);
        d0 = abs(d0)-lw;

        vec2 p1 = p;
        p1 -= vec2(0.175, -0.445);
        p1.y = -p1.y;
        p1  = refl(p1, normalize(vec2(1.0)));
        float d1 = parallelogram(p1, 0.31, lw, 0.04);

        vec2 p2 = p;
        float d2 = p2.y+0.20;
        d1 = pmax(d1, d2, sm);

        vec2 p3 = p;
        float d3 = min(-p3.y+0.075, -p3.x+0.105);
        d0 = pmax(d0, d3, sm);

        vec2 p4 = p;
        p4 -= vec2(0.175, -0.445)-vec2(lw);
        float d4 = length(p4);
        d1 = sign(p4) == vec2(-1.0) ? d4 : d1;

        float d = d0;
        d = min(d, d1);
      //  d = min(d, d4);

        return d;
      }

      float df(vec2 p) {
        return omega(p);
      }

      float height_(vec2 p) {
        p *= 0.25;
        vec2 p0 = p;
        vec2 n0 = hextile(p0);
        float h = hash(n0);
        p0 *= ROT(TAU*h);
        const float ss = 0.625;
        float d0 = omega(p0/ss)*ss;
        float d = d0;
        d -= 0.02;
        const float aa = 0.02;
        float a = fract(127.0*h) > 0.5 ? 0.125 : -0.25;
        return a*tanh_approx(smoothstep(aa, -aa, d));
      }

      float height(vec2 p) {
        const mat2 rot1 = ROT(1.23);
        float tm = 123.0+TTIME/320.0;
        p += 5.0*vec2(cos(tm), sin(tm*sqrt(0.5)));
        const float aa = -0.33;
        const mat2  pp = (1.0/aa)*rot1;
        float h = 0.0;
        float a = 1.0;
        float d = 0.0;

        float tm2 = 0.5*TIME;
        h += 1.0*sin(0.75*tm2+dot(normalize(vec2(-1.0, 1.5)), 4.0*p));
        h += 0.5*sin(tm2+dot(normalize(vec2(1.0, 1.0)), 7.0*p));
        h += 0.25*sin(1.25*tm2+dot(normalize(vec2(-2, 1.0)), 13.0*p));
        h += 1.75;
        h*= 0.2;

        for (int i = 0; i < 5; ++i) {
          h += a*height_(p);
          d += a;
          a *= aa;
          p *= pp;
        }
        const float hf = -0.125;
        return hf*(h/d)+hf;
      }

      vec3 normal(vec2 p) {
        vec2 v;
        vec2 w;
        vec2 e = vec2(4.0/RESOLUTION.y, 0);

        vec3 n;
        n.x = height(p + e.xy) - height(p - e.xy);
        n.y = 2.0*e.x;
        n.z = height(p + e.yx) - height(p - e.yx);

        return normalize(n);
      }

      float synth(vec2 p) {
        const float z = 4.0;
        const float st = 0.02;
        float dob = box(p, vec2(1.55, 0.5));
        p.x = abs(p.x);
        p.x += st*20.0;
        p /= z;
        float n = mod1(p.x, st);
        float dib = 1E6;
        const int around = 1;
        for (int i = -around; i <=around ;++i) {
          float fft = texture(frequency_data, vec2((n+float(i))*st*1.5, 0.5)).x;
          float dibb = box(p-vec2(st*float(i), 0.0), vec2(st*0.25, 0.05*fft+0.001));
          dib = min(dib, dibb);
        }

        float dl = p.y;
        dl = abs(dl) - 0.005;
        dl = abs(dl) - 0.0025;
        dl = abs(dl) - 0.00125;
        float d = dib;
        d = max(d, -dl);
        d = pmax(d, dob, 0.025);
        return d*z;
      }

      float omega_a(vec2 p) {
        p.x = abs(p.x);
        float db = roundedBox(p, vec2 (0.19, 0.166), vec4(corner1, corner0, corner1, corner0));
        float dc = corner(p-vec2(0.045, -0.07))-corner0;

        float d = db;
        d = max(d, -dc);

        return d;
      }

      float omega_e(vec2 p) {
        p.y = abs(p.y);
        float db = roundedBox(p, vec2 (0.19, 0.166), vec4(corner1, corner0, corner1, corner0));
        float di = roundedBox(p-vec2(0.175, topy), vec2 (0.25, corner0), vec4(corner0));

        float d = db;
        d = max(d, -di);
        return d;
      }

      float omega_g(vec2 p) {
        float db = roundedBox(p, vec2 (0.19, 0.166), vec4(corner0, corner1, corner1, corner1));
        float dc = corner(-(p-vec2(-0.045, -0.057)));
        dc = abs(dc) - corner0;
        float dd = max(p.x-0.065, p.y-topy-corner0);
        float d = db;
        d = max(d, -max(dc, dd));
        return d;
      }

      float omega_m(vec2 p) {
        float db = roundedBox(p, vec2 (0.255, 0.166), vec4(corner1, corner0, corner0, corner0));
        p.x = abs(p.x);
        float dl = abs(p.x-0.095)-corner0*2.0;
        float dt = p.y-topy;

        float d = db;
        d = max(d, -max(dl,dt));

        return d;
      }

      float omega_o(vec2 p) {
        float db = roundedBox(p, vec2 (0.19, 0.166), vec4(corner1));
        float di = roundedBox(p, vec2 (0.19, 0.166)*0.45, vec4(corner0));
        float d = db;
        d = max(d, -di);
        return d;
      }

      float omegaText(vec2 p) {
        vec2 pn = p;
        pn.y -= topy;
        float dn = abs(pn.y)-corner0;
        p.x += 0.95;

        vec2 po = p;
        po.x += 0.0;
        float do_ = omega_o(po);

        vec2 pm = p;
        pm.x -= 0.51;
        float dm = omega_m(pm);

        vec2 pe = p;
        pe.x -= 1.02;
        float de = omega_e(pe);

        vec2 pg = p;
        pg.x -= 1.465;
        float dg = omega_g(pg);

        vec2 pa = p;
        pa.x -= 1.91;
        float da = omega_a(pa);


        float d = 1E6;
        d = min(d, do_);
        d = min(d, dm);
        d = min(d, de);
        d = min(d, dg);
        d = min(d, da);
        d = max(d, -dn);

        return d;
      }

      float beat() {
        float beat = 0.0;
        const float samples = 5.0;
        const float st = 0.01;
        for (float i = 0.0; i < samples; ++i) {
          beat += texture(frequency_data, vec2(st*1.0+st*i, 0.5)).x;
        }
        return smoothstep(0.66, 1.0, beat/samples);
      }

      // Post processing I found somewhere on shadertoy years ago
      vec3 postProcess(vec3 col, vec2 q) {
        col = clamp(col, 0.0, 1.0);
        col = pow(col, 1.0/vec3(2.2));
        col = col*0.6+0.4*col*col*(3.0-2.0*col);
        col = mix(col, vec3(dot(col, vec3(0.33))), -0.4);
        col *=0.5+0.5*pow(19.0*q.x*q.y*(1.0-q.x)*(1.0-q.y),0.7);
        return col;
      }

      vec3 effect(vec2 p, vec2 q) {
        vec2 ppp = p;
        const float s = 1.0;
        const vec3 lp1 = vec3(1.0, 1.25, 1.0)*vec3(s, 1.0, s);
        const vec3 lp2 = vec3(-1.0, 1.25, 1.0)*vec3(s, 1.0, s);

        float aa = 2.0/RESOLUTION.y;

        float h = height(p);
        vec3  n = normal(p);

        vec3 ro = vec3(0.0, mix(-3.0, -10.0, PSIN(TIME+PI*p.x)), 0.0);
        vec3 pp = vec3(p.x, 0.0, p.y);

        vec3 po = vec3(p.x, h, p.y);
        vec3 rd = normalize(ro - po);

        vec3 ld1 = normalize(lp1 - po);
        vec3 ld2 = normalize(lp2 - po);

        float diff1 = max(dot(n, ld1), 0.0);
        float diff2 = max(dot(n, ld2), 0.0);

        vec3  rn    = n;
        vec3  ref   = reflect(rd, rn);
        float ref1  = max(dot(ref, ld1), 0.0);
        float ref2  = max(dot(ref, ld2), 0.0);

        const vec3 lcol1 = sqrt(vec3(1.25, 1.35, 2.0).zxy*0.15);
        const vec3 lcol2 = vec3(2.0, 1.55, 1.25).zyx*0.85;
        vec3 lpow1 = 0.15*lcol1/L2(ld1);
        vec3 lpow2 = 0.25*lcol2/L2(ld2);
        vec3 dm = vec3(1.0)*tanh_approx(-h*10.0+0.125);
        vec3 col = vec3(0.0);
        col += dm*pow(diff1, 4.0)*lpow1;
        col += dm*pow(diff2, 4.0)*lpow2;
        vec3 rm = vec3(1.0)*mix(0.25, 1.0, tanh_approx(-h*1000.0));
        col += rm*pow(ref1, 20.0)*lcol1;
        col += rm*pow(ref2, 20.0)*lcol2;

        float ds = synth(ppp-vec2(0.0, -0.75));
        float beat = beat();

        const float zp = 1.35;

        vec2 op = ppp;
        op.x += -0.25*ppp.y;
        op /= zp;
        float di = omegaText(op);
        di *= zp;
        float dio = di;
        dio -= 0.025;
        dio = abs(dio) - 0.005;
        di = pmin(di, dio, 0.025);
        di = min(di, ds);
        float dg = di;

        float off = -0.3 + 0.75*p.x-p.y;
        float angle = -0.5+off-TTIME/16.0;

        float gmix = pow(PSIN(angle), 14.0);
        float gmix2 = mix(1.0, abs(1.0/(mod(angle, PI)-PI*0.5)), beat*smoothstep(1.75, 1.55, abs(off)));
        col += -lcol2*0.25*(exp(-9.0*max(di, 0.0)));

        dg = abs(dg-0.025);
        float glow = exp(-20.0*max(dg+0., 0.0));
        vec3 glowCol = mix(lcol2.zyx*lcol2.zyx/6.0, lcol2.zyx, glow*glow)*gmix2;
        col = mix(col, vec3(mix(vec3(0.9), sqrt(glowCol*0.5), gmix)), smoothstep(-aa, aa, -di));
        col += glowCol*glow*gmix;
        col = postProcess(col, q);

        return col;
      }

      void main() {
        vec2 q = v_texcoord;
        vec2 p = -1.0 + 2.0*q;
        p.x *= RESOLUTION.x/RESOLUTION.y;

        vec3 col = effect(p, q);

        frag_color = vec4(col,1.0);
      }
    </script>

    <script type = "text/javascript">
      const analyze_audio   = true;
      const demo_system     = new DemoSystemV2(analyze_audio);

      const global_uniforms = [
      ];

      function override_nearest(image) {
        return [image, (gl) => {
          gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MAG_FILTER, gl.NEAREST);
//          gl.texParameteri(gl.TEXTURE_2D, gl.TEXTURE_MIN_FILTER, gl.NEAREST_MIPMAP_NEAREST);
        }];
      }

      const all_textures = {
        tribute__1989 : {
          image : (gl) => override_nearest(document.getElementById("tribute__1989")),
        },
        tribute__almost_mono : {
          image : (gl) => document.getElementById("tribute__almost_mono"),
        },
        tribute__red_sector : {
          image : (gl) => document.getElementById("tribute__red_sector"),
        },
        tribute__romeo_knight : {
          image : (gl) => document.getElementById("tribute__romeo_knight"),
        },
        tribute__texts_df : {
          image : (gl) => document.getElementById("tribute__texts_df"),
        },
      }

      const all_scenes = {
        red_scene : {
          passes:[
            {
              vs: "vs_default",
              fs: "fs_red"    ,
            },
          ],
        },
        moody_scene : {
          passes:[
            {
              vs: "vs_default",
              fs: "fs_moody"  ,
              pre_render: (gl, time, scene, pass) => {
                // Texture 0-3 is reserved
                gl.activeTexture(gl.TEXTURE4);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__1989.texture);
                gl.uniform1i(pass.uniformLocations.image, 4);

                gl.activeTexture(gl.TEXTURE5);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__texts_df.texture);
                gl.uniform1i(pass.uniformLocations.texts, 5);
              },
            }
          ],
          uniforms: ["image", "texts"],
        },
        intro_scene : {
          passes:[
            {
              vs: "vs_default",
              fs: "fs_intro"  ,
              pre_render: (gl, time, scene, pass) => {
                // Texture 0-3 is reserved
                gl.activeTexture(gl.TEXTURE4);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__almost_mono.texture);
                gl.uniform1i(pass.uniformLocations.almost_mono, 4);

                gl.activeTexture(gl.TEXTURE5);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__red_sector.texture);
                gl.uniform1i(pass.uniformLocations.red_sector, 5);

                gl.activeTexture(gl.TEXTURE6);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__texts_df.texture);
                gl.uniform1i(pass.uniformLocations.texts, 6);
              },
            }
          ],
          uniforms: ["almost_mono", "red_sector", "texts"],
        },
        beatbox_scene_0 : {
          passes:[
            {
              vs: "vs_default"  ,
              fs: "fs_beatbox"  ,
            }
          ],
          defines:["DF0"]
        },
        beatbox_scene_1 : {
          passes:[
            {
              vs: "vs_default"  ,
              fs: "fs_beatbox"  ,
            }
          ],
          defines:["DF4"]
        },
        beatbox_scene_2 : {
          passes:[
            {
              vs: "vs_default"  ,
              fs: "fs_beatbox"  ,
            }
          ],
          defines:["DF6"]
        },
        dawn_scene : {
          passes:[
            {
              vs: "vs_default"  ,
              fs: "fs_dawn"     ,
            }
          ],
          defines:["DF4"]
        },
        romeo_knight_scene : {
          passes:[
            {
              vs: "vs_default"      ,
              fs: "fs_romeo_knight" ,
              pre_render: (gl, time, scene, pass) => {
                // Texture 0-3 is reserved
                gl.activeTexture(gl.TEXTURE4);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__romeo_knight.texture);
                gl.uniform1i(pass.uniformLocations.image, 4);

                gl.activeTexture(gl.TEXTURE5);
                gl.bindTexture(gl.TEXTURE_2D, all_textures.tribute__texts_df.texture);
                gl.uniform1i(pass.uniformLocations.texts, 5);
              },
            }
          ],
          uniforms: ["image", "texts"],
        },
        solo_scene : {
          passes:[
            {
              vs: "vs_default"  ,
              fs: "fs_solo"     ,
            }
          ],
        },
      };

      const bstart  = 25.61;
      const bpm     = 125.0;
      const bhz     = bpm/60.0;
      function btime(b) {
        return (8.0*b/bhz+bstart);
      }

      const max_time_slots  = 128;
      const time_slots      = new Array(max_time_slots);
      const script = [
        {
          begin       :  0                                ,
          scene       :  all_scenes.intro_scene           ,
        },
        {
          begin       :  3                                ,
          scene       :  all_scenes.beatbox_scene_0       ,
        },
        {
          begin       :  5                                ,
          scene       :  all_scenes.beatbox_scene_1       ,
        },
        {
          begin       :  7                                ,
          scene       :  all_scenes.beatbox_scene_2       ,
        },
        {
          begin       :  9                                ,
          scene       :  all_scenes.dawn_scene            ,
        },
        {
          begin       :  13                               ,
          scene       :  all_scenes.romeo_knight_scene    ,
        },
        {
          begin       :  15                               ,
          scene       :  all_scenes.solo_scene            ,
        },
        {
          begin       :  64                               ,
          scene       :  all_scenes.red_scene             ,
        },
      ];

      function populate_time_slots() {
        let current_script = script[0];
        let slot = 0;
        for (const idx in script) {
          const next_script = script[idx];
          while(slot < max_time_slots && slot < next_script.begin) {
            time_slots[slot] = current_script;
            ++slot;
          }
          current_script = next_script;
        }
        while(slot < max_time_slots) {
            time_slots[slot] = current_script;
            ++slot;
        }
      }
      populate_time_slots();

      function get_slot(time) {
        if (time < bstart) {
          return all_scenes.moody_scene;
        }
        const i = clamp(Math.floor((time-bstart)*bhz/8.0), 0, max_time_slots-1);
        return time_slots[i];
      }

      function handleKeyDown(event) {
        if (event.altKey && event.key === 'Enter') {
          event.preventDefault();
          const music = document.getElementById("music");
          music.play();
        }
      }

      function on_init_complete(width, height) {
        const label = document.getElementById("info_label");
        label.textContent = "🚀 Ready, click play to run or hit Alt+Enter";

        const music = document.getElementById("music");
        music.style.visibility = "visible";

        document.addEventListener('keydown', handleKeyDown);
      }

      // Called after user clicked play
      function on_started() {
        const information = document.getElementById("information");
        information.style.display = "none";
      }
      // Called before each shader is compiled
      function on_loading_scene(key) {
        const label = document.getElementById("info_label");
        label.textContent = "🔄 Compiling: " + key;
      }

      function pre_render(gl, time, scene, pass) {
      }

      // Called each frame to select which scene to render
      function on_select_scene(gl, time) {
        const slot = get_slot(time);
        if(slot && slot.scene) {
          return slot.scene;
        } else {
          return all_scenes.moody_scene;
        }
      }

    </script>
  </head>

  <body onload="demo_system.run_demo()">
    <audio id="music" style="visibility: hidden;" controls src="soundlogic_-_rise_up_-_amigaremix_03445.mp3"></audio>
    <div id="information" style="margin-left: 16pt;">
      <h1>Our tribute to Red Sector</h1>

      <p id="info_label">Please wait... </p>
    </div>
    <div class="center-flex">
      <div>
        <canvas id="screen_canvas" class="gl">
          Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
        </canvas>
      </div>
    </div>
    <img id="tribute__1989" src="tribute__1989.jpg" style="display: none"/>
    <img id="tribute__almost_mono" src="tribute__almost_mono.jpg" style="display: none"/>
    <img id="tribute__red_sector" src="tribute__red_sector.jpg" style="display: none"/>
    <img id="tribute__romeo_knight" src="tribute__romeo_knight.jpg" style="display: none"/>
    <img id="tribute__texts_df" src="tribute__texts_df.png" style="display: none"/>
  </body>
</html>